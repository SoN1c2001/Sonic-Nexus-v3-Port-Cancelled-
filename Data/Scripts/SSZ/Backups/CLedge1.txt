//------------Sonic Nexus Crumbling Ledge-------------//
//----Scripted by Christian Whitehead "The Taxman"----//

#alias Object.Value0 : LedgeLeft
#alias Object.Value1 : LedgeRight
#alias Object.Value2 : Object.Timer
#alias Object.Value3 : NoBlocks
#alias Object.Value4 : Object.YVelocity

#alias 50 : TYPE_CLEDGE

sub ObjectMain

	switch Object.State
	case 1	//Wait Right Side
		if Object.Value2>0
			Object.Value2--
		else
			PlayStageSfx(1,false)
			Object.State=4
		end if
		break

	case 2	//Wait Left Side
		if Object.Value2>0
			Object.Value2--
		else
			PlayStageSfx(1,false)
			Object.State=3
		end if
		break

	case 3	//Crumble from Left Side
		Object.Value2++
		if Object.Value2==6
			Object.Value2=0
			if Object.Value0<Object.Value1
				Object.Value3--
				Object.Value0+=16

				TempValue0=Object.Value0
				TempValue0-=8
				TempValue0<<=16
				TempValue0+=Object.XPos
				TempValue1=Object.YPos
				TempValue1-=655360
				CreateTempObject(TYPE_CLEDGE,0,TempValue0,TempValue1)
				Object[TempObjectPos].State=6
				Object[TempObjectPos].Value4=-65536
				Object[TempObjectPos].Frame=1
				TempValue1+=1310720
				CreateTempObject(TYPE_CLEDGE,0,TempValue0,TempValue1)
				Object[TempObjectPos].State=6
				Object[TempObjectPos].Frame=2
			else
				//Object.State=5
				//Object.Priority=1
				ResetObjectEntity(Object.EntityNo,0,0,0,0)
			end if
		end if
		break

	case 4	//Crumble from Right Side
		Object.Value2++
		if Object.Value2==6
			Object.Value2=0
			if Object.Value1>Object.Value0
				Object.Value3--
				Object.Value1-=16

				TempValue0=Object.Value1
				TempValue0+=8
				TempValue0<<=16
				TempValue0+=Object.XPos
				TempValue1=Object.YPos
				TempValue1-=655360
				CreateTempObject(TYPE_CLEDGE,0,TempValue0,TempValue1)
				Object[TempObjectPos].State=6
				Object[TempObjectPos].Value4=-65536
				Object[TempObjectPos].Frame=1
				TempValue1+=1310720
				CreateTempObject(TYPE_CLEDGE,0,TempValue0,TempValue1)
				Object[TempObjectPos].State=6
				Object[TempObjectPos].Frame=2
			else
				//Object.State=5
				//Object.Priority=1
				ResetObjectEntity(Object.EntityNo,0,0,0,0)
			end if
		end if
		break

	case 5 //Wait for Respawn
		if Object.OutOfBounds==true
			Object.State=0
			Object.Value3=Object.PropertyValue
			Object.Value0=Object.PropertyValue
			Object.Value0<<=3
			Object.Value1=Object.Value0
			FlipSign(Object.Value0)
			Object.Priority=0
			Object.Value2=0
		end if
		break

	case 6 //Falling Block
		Object.Value4+=16384
		if Object.Value4>0
			Object.YPos+=Object.Value4
		end if
		if Object.OutOfBounds==true
			Object.Type=0
		end if
		break

	end switch

end sub

sub ObjectPlayerInteraction
	
	if Object.State==0
		PlayerObjectCollision(C_PLATFORM,Object.Value0,-16,Object.Value1,16)
		if CheckResult==1
			Object.Value2=15
			Object.Priority=1
			if Player.XPos>Object.XPos
				Object.State=1
			else
				Object.State=2
			end if
		end if
	else
		PlayerObjectCollision(C_PLATFORM,Object.Value0,-16,Object.Value1,16)
	end if

end sub

sub ObjectDraw

	switch Object.State
	case 0
	case 1
	case 2
	case 4
		TempValue0=0
		TempValue1=Object.Value0
		TempValue1<<=16
		TempValue1+=524288
		TempValue1+=Object.XPos

		while TempValue0<Object.Value3
			DrawSpriteXY(0,TempValue1,Object.YPos)
			TempValue0++
			TempValue1+=1048576
		loop
		break
	case 3
		TempValue0=0
		TempValue1=Object.Value1
		TempValue1<<=16
		TempValue1-=524288
		TempValue1+=Object.XPos

		while TempValue0<Object.Value3
			DrawSpriteXY(0,TempValue1,Object.YPos)
			TempValue0++
			TempValue1-=1048576
		loop
		break

	case 6
		DrawSprite(Object.Frame)
		break
	end switch

end sub

sub ObjectStartup
	
	LoadSpriteSheet("SSZ/Gimmicks.gif")

	//Setup the Ledge sizes for any Crumbling Ledge
	//Objects that are present in the level
	
	ArrayPos0=32
	while ArrayPos0<1056
		if Object[ArrayPos0].Type==TYPE_CLEDGE
			Object.Value3[ArrayPos0]=Object[ArrayPos0].PropertyValue
			Object.Value0[ArrayPos0]=Object[ArrayPos0].PropertyValue
			Object.Value0[ArrayPos0]<<=3
			Object.Value1[ArrayPos0]=Object.Value0[ArrayPos0]
			FlipSign(Object.Value0[ArrayPos0])
		end if
		ArrayPos0++
	loop

	//Ledge Frame
	SpriteFrame(-8,-20,16,40,1,150)

	//Broken Ledge Frames
	SpriteFrame(-8,-10,16,20,1,150)
	SpriteFrame(-8,-10,16,20,1,170)
	
end sub

sub RSDK
	LoadSpriteSheet("SSZ/Gimmicks.gif")
	SetEditorIcon(Icon0,RepeatH,-8,-20,16,40,1,150)
end sub