//---------------Sonic Nexus SignPost-----------------//
//----Scripted by Christian Whitehead "The Taxman"----//

#alias 5 : TYPE_ACTFINISH
#alias Object.Value0 : Object.NoSpins
#alias Object.Value1 : Object.Count

sub ObjectMain

	switch Object.State
	case 0
		Object.Frame=1

		Stage.NewXBoundary1=Object.XPos
		Stage.NewXBoundary1>>=16
		Stage.NewXBoundary1-=160
		Stage.NewXBoundary2=Stage.NewXBoundary1
		Stage.NewXBoundary2+=364

		Stage.NewYBoundary2=Object.YPos
		Stage.NewYBoundary2>>=16
		Stage.NewYBoundary2+=80
		Stage.NewYBoundary1=Stage.NewYBoundary2
		Stage.NewYBoundary1-=240

		if Player.XPos>Object.XPos
			//Stop Time
			Stage.TimeEnabled=false
			Object.State=1
			Object.NoSpins=10
			PlaySfx(12,false)
		end if
		break
	case 1
		Music.Volume--
		Object.Count++
		if Object.Count==2
			Object.Count=0
			Object.Frame++
			if Object.Frame==5
				
				Rand(TempValue0,48)
				TempValue0<<=16
				TempValue0+=Object.XPos
				TempValue0-=1572864

				Rand(TempValue1,32)
				TempValue1<<=16
				TempValue1+=Object.YPos
				TempValue1-=1835008

				CreateTempObject(8,0,TempValue0,TempValue1)

				Object.NoSpins--
				
				if Object.NoSpins==0
					PlayMusic(1)
					Player.ControlMode=-1
					Player.Up=false
					Player.Down=false
					Player.Left=false
					Player.JumpPress=false
					Player.JumpHold=false
					Object.State=2
					Object[30].Type=TYPE_ACTFINISH
					Object[30].DrawOrder=6
					Object[30].Priority=1
				end if

			end if
			if Object.Frame==9

				Rand(TempValue0,48)
				TempValue0<<=16
				TempValue0+=Object.XPos
				TempValue0-=1572864

				Rand(TempValue1,32)
				TempValue1<<=16
				TempValue1+=Object.YPos
				TempValue1-=1835008

				CreateTempObject(8,0,TempValue0,TempValue1)
				Object.Frame=1

			end if
		end if
		TempValue0=Object.XPos
		TempValue0+=20971520
		if Player.XPos>TempValue0
			Player.XPos=TempValue0
			Player.Speed=0
			Player.XVelocity=0
		end if
		break
	case 2
		if Player.ControlMode==-1
			Player.Right=true
		end if
		TempValue0=Object.XPos
		TempValue0+=20971520
		if Player.XPos>TempValue0
			Player.XPos=TempValue0
			Player.Speed=0
			Player.XVelocity=0
		end if
		break
	end switch

end sub

sub ObjectDraw
	DrawSprite(0)
	DrawSprite(Object.Frame)
end sub

sub ObjectStartup
	LoadSpriteSheet("NexusGlobals/Items.gif")
	
	//Pole
	SpriteFrame(-12,4,24,24,175,166)

	//Sign Frames
	SpriteFrame(-24,-28,48,32,208,178)
	SpriteFrame(-16,-28,32,32,175,191)
	SpriteFrame(-4,-28,8,32,175,133)
	SpriteFrame(-16,-28,32,32,175,224)
	if Stage.PlayerListPos==0
		SpriteFrame(-24,-28,48,32,208,211)
	else
		SpriteFrame(-24,-28,48,32,208,211)
	endif
	SpriteFrame(-16,-28,32,32,175,191)
	SpriteFrame(-4,-28,8,32,175,133)
	SpriteFrame(-16,-28,32,32,175,224)
end sub

sub RSDK
	LoadSpriteSheet("NexusGlobals/Items.gif")
	SetEditorIcon(Icon0,SingleIcon,-12,4,24,24,175,166)
end sub