//----------------Sonic Nexus Bridge------------------//
//----Scripted by Christian Whitehead "The Taxman"----//

sub ObjectPlayerInteraction
	if Player.XPos>Object.Value6
		
		if Player.XPos<Object.Value7
			
			if Player.EntityNo==Object.Value5
				
				Object.Value2=Player.XPos
				Object.Value2-=Object.Value6
				TempValue0=Object.Value2
				TempValue0>>=8

				TempValue1=Object.Value7
				TempValue1-=Object.Value6
				TempValue2=TempValue1
				TempValue2>>=16

				TempValue0/=TempValue2
				
				Sin(Object.Value3,TempValue0)
				TempValue1>>=13
				Object.Value3*=TempValue1
				
				TempValue0=Object.YPos
				TempValue0-=3145728
				if Player.YPos>TempValue0
					if Player.YVelocity>=0
						TempValue2=Player.CollisionBottom
						FlipSign(TempValue2)
						TempValue2<<=16
						TempValue2+=Object.Value4
						TempValue2-=524288
						
						Object.Value0++
						Player.YPos=Object.YPos
						Player.YPos+=TempValue2
						Player.Gravity=false
						Player.YVelocity=0
						Player.Flailing[0]=true
						Player.Flailing[1]=true
						Player.Flailing[2]=true
					end if
				end if
				
			else
				
				if Player.YVelocity>=0
					
					TempValue0=Player.XPos
					TempValue0-=Object.Value6
					
					if TempValue0>Object.Value2
						TempValue0=Object.Value7
						TempValue0-=Player.XPos
						
						TempValue3=Object.Value7
						TempValue3-=Object.Value6
						TempValue3-=Object.Value2
						
						TempValue1=TempValue0
						TempValue1<<=7
						TempValue1/=TempValue3
					else
						TempValue1=TempValue0
						TempValue1<<=7
						TempValue1/=Object.Value2
					end if
					
					Sin(TempValue2,TempValue1)
					TempValue2*=Object.Value4
					TempValue2>>=9
					TempValue2-=524288
					
					if Player.YVelocity<32768
						TempValue3=TempValue2
						TempValue3>>=16
						TempValue4=TempValue3
						TempValue3-=8
					else
						TempValue3=TempValue2
						TempValue3>>=16
						TempValue4=TempValue3
						TempValue4+=8
					end if
					
					PlayerObjectCollision(C_TOUCH,-1024,TempValue3,1024,TempValue4)
					
					if CheckResult==1
						Object.Value0++
						Player.YPos=Player.CollisionBottom
						FlipSign(Player.YPos)
						Player.YPos<<=16
						Player.YPos+=Object.YPos
						Player.YPos+=TempValue2
						
						Player.Flailing[0]=true
						Player.Flailing[1]=true
						Player.Flailing[2]=true
						
						if Player.Gravity==false
							Object.Value1=128
						else
							Player.Gravity=false
						end if
						Player.YVelocity=0
						
						if Player.EntityNo==0
							Object.Value5=0
						end if
					end if
					
				end if
				
			end if
			
		end if
		
	end if
	
end sub

sub ObjectDraw
	
	if Object.Value0>0
		if Object.Value1<128
			Object.Value1+=8
		end if
	else
		if Object.Value1>0
			Object.Value1-=8
			Object.Value5=-1
		end if
	end if
	
	Object.Value4=Object.Value3
	Object.Value4*=Object.Value1
	Object.Value4>>=7
	
	Object.Value0=0
	
	TempValue0=0
	TempValue1=Object.Value6
	TempValue1+=524288
	
	TempValue4=524288
	
	TempValue5=Object.Value2
	TempValue5>>=20
	
	while TempValue0<TempValue5
		
		TempValue3=TempValue4
		TempValue3<<=7
		TempValue3/=Object.Value2
		
		Sin(TempValue2,TempValue3)
		TempValue2*=Object.Value4
		TempValue2>>=9
		
		TempValue2+=Object.YPos
		
		DrawSpriteXY(0,TempValue1,TempValue2)
		TempValue1+=1048576
		TempValue4+=1048576
		TempValue0++
	loop
	
	TempValue2=Object.Value4
	TempValue2+=Object.YPos
	DrawSpriteXY(0,TempValue1,TempValue2)
	TempValue1+=1048576
	TempValue0++
	
	TempValue5=Object.Value7
	TempValue5-=Object.Value6
	TempValue5-=Object.Value2
	
	TempValue1=Object.Value7
	TempValue1-=524288
	
	TempValue4=524288
	
	while TempValue0<Object.PropertyValue
		
		TempValue3=TempValue4
		TempValue3<<=7
		TempValue3/=TempValue5
		
		Sin(TempValue2,TempValue3)
		TempValue2*=Object.Value4
		TempValue2>>=9
		
		TempValue2+=Object.YPos
		
		DrawSpriteXY(0,TempValue1,TempValue2)
		TempValue1-=1048576
		TempValue4+=1048576
		TempValue0++
		
	loop
	
end sub

sub ObjectStartup
	
	LoadSpriteSheet("SSZ/Objects.gif")
	
	//Log Frame
	SpriteFrame(-8,-8,16,16,18,78)
	
	//Setup the Bridge sizes for any Bridge
	//Objects that are present in the level
	
	ArrayPos0=32
	while ArrayPos0<1056
		if Object[ArrayPos0].Type==TypeName[Bridge]
			Object.Value6[ArrayPos0]=Object[ArrayPos0].PropertyValue
			Object.Value6[ArrayPos0]<<=19
			Object.Value7[ArrayPos0]=Object.Value6[ArrayPos0]
			FlipSign(Object.Value6[ArrayPos0])
			Object.Value6[ArrayPos0]+=Object[ArrayPos0].XPos
			Object.Value7[ArrayPos0]+=Object[ArrayPos0].XPos
			Object.Value5[ArrayPos0]=-1
		end if
		ArrayPos0++
	loop
	
end sub

sub RSDK
	LoadSpriteSheet("SSZ/Objects.gif")
	SetEditorIcon(Icon0,RepeatH,-8,-8,16,16,84,124)
end sub