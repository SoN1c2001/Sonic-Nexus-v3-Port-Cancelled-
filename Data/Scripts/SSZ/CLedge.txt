//------------Sonic Nexus Crumbling Ledge-------------//
//----Scripted by Christian Whitehead "The Taxman"----//

#alias Object.Value0 : LedgeLeft
#alias Object.Value1 : LedgeRight
#alias Object.Value2 : Object.Timer
#alias Object.Value3 : NoBlocks
#alias Object.Value4 : Object.YVelocity

sub ObjectMain
	switch Object.State
	case 1	//Wait Right Side
		if Object.Timer>0
			Object.Timer--
		else
			PlayStageSfx(0,false)
			Object.State=4
		end if
		break
	case 2	//Wait Left Side
		if Object.Timer>0
			Object.Timer--
		else
			PlayStageSfx(0,false)
			Object.State=3
		end if
		break
	case 3	//Crumble from Left Side
		Object.Timer++
		if Object.Timer==6
			Object.Timer=0
			if LedgeLeft<LedgeRight
				NoBlocks--
				LedgeLeft+=16

				TempValue0=LedgeLeft
				TempValue0-=8
				TempValue0<<=16
				TempValue0+=Object.XPos
				TempValue1=Object.YPos
				TempValue1-=655360
				CreateTempObject(TypeName[CLedge],0,TempValue0,TempValue1)
				Object[TempObjectPos].State=6
				Object[TempObjectPos].YVelocity=-65536
				Object[TempObjectPos].Frame=1
				TempValue1+=1310720
				CreateTempObject(TypeName[CLedge],0,TempValue0,TempValue1)
				Object[TempObjectPos].State=6
				Object[TempObjectPos].Frame=2
			else
				//Object.State=5
				//Object.Priority=1
				ResetObjectEntity(Object.EntityNo,0,0,0,0)
			end if
		end if
		break
	case 4	//Crumble from Right Side
		Object.Timer++
		if Object.Timer==6
			Object.Timer=0
			if LedgeRight>LedgeLeft
				NoBlocks--
				LedgeRight-=16

				TempValue0=LedgeRight
				TempValue0+=8
				TempValue0<<=16
				TempValue0+=Object.XPos
				TempValue1=Object.YPos
				TempValue1-=655360
				CreateTempObject(TypeName[CLedge],0,TempValue0,TempValue1)
				Object[TempObjectPos].State=6
				Object[TempObjectPos].YVelocity=-65536
				Object[TempObjectPos].Frame=1
				TempValue1+=1310720
				CreateTempObject(TypeName[CLedge],0,TempValue0,TempValue1)
				Object[TempObjectPos].State=6
				Object[TempObjectPos].Frame=2
			else
				//Object.State=5
				//Object.Priority=1
				ResetObjectEntity(Object.EntityNo,0,0,0,0)
			end if
		end if
		break
	case 5 //Wait for Respawn
		if Object.OutOfBounds==true
			Object.State=0
			NoBlocks=Object.PropertyValue
			LedgeLeft=Object.PropertyValue
			LedgeLeft<<=3
			LedgeRight=LedgeLeft
			FlipSign(LedgeLeft)
			Object.Priority=0
			Object.Timer=0
		end if
		break
	case 6 //Falling Block
		Object.YVelocity+=16384
		if Object.YVelocity>0
			Object.YPos+=Object.YVelocity
		end if
		if Object.OutOfBounds==true
			Object.Type=0
		end if
		break
	end switch
end sub

sub ObjectPlayerInteraction
	if Object.State==0
		PlayerObjectCollision(C_PLATFORM,LedgeLeft,-16,LedgeRight,16)
		if CheckResult==1
			Object.Timer=15
			Object.Priority=1
			if Player.XPos>Object.XPos
				Object.State=1
			else
				Object.State=2
			end if
		end if
	else
		PlayerObjectCollision(C_PLATFORM,LedgeLeft,-16,LedgeRight,16)
	end if
end sub

sub ObjectDraw
	switch Object.State
	case 0
	case 1
	case 2
	case 4
		TempValue0=0
		TempValue1=LedgeLeft
		TempValue1<<=16
		TempValue1+=524288
		TempValue1+=Object.XPos

		while TempValue0<NoBlocks
			DrawSpriteXY(0,TempValue1,Object.YPos)
			TempValue0++
			TempValue1+=1048576
		loop
		break
	case 3
		TempValue0=0
		TempValue1=LedgeRight
		TempValue1<<=16
		TempValue1-=524288
		TempValue1+=Object.XPos

		while TempValue0<NoBlocks
			DrawSpriteXY(0,TempValue1,Object.YPos)
			TempValue0++
			TempValue1-=1048576
		loop
		break
	case 6
		DrawSprite(Object.Frame)
		break
	end switch
end sub

sub ObjectStartup
	LoadSpriteSheet("SSZ/Objects2.gif")

	//Setup the Ledge sizes for any Crumbling Ledge
	//Objects that are present in the level
	
	ArrayPos0=32
	while ArrayPos0<1056
		if Object[ArrayPos0].Type==TypeName[CLedge]
			NoBlocks[ArrayPos0]=Object[ArrayPos0].PropertyValue
			LedgeLeft[ArrayPos0]=Object[ArrayPos0].PropertyValue
			LedgeLeft[ArrayPos0]<<=3
			LedgeRight[ArrayPos0]=LedgeLeft[ArrayPos0]
			FlipSign(LedgeLeft[ArrayPos0])
		end if
		ArrayPos0++
	loop

	//Ledge Frame
	SpriteFrame(-8,-20,16,40,257,48)

	//Broken Ledge Frames
	SpriteFrame(-8,-10,16,20,257,48)
	SpriteFrame(-8,-10,16,20,257,68)
end sub

sub RSDK
	LoadSpriteSheet("SSZ/Objects.gif")
	SetEditorIcon(Icon0,RepeatH,-8,-20,16,40,231,1)
end sub