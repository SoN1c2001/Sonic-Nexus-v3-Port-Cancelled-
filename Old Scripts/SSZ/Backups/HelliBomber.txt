//----------Sonic Nexus Hell-i-bomber Badnik----------//
//----Scripted by Christian Whitehead "The Taxman"----//

#alias Object.Value0 : Object.XVelocity
#alias Object.Value1 : Object.YVelocity
#alias Object.Value2 : Object.DestX
#alias Object.Value3 : Object.Timer
#alias Object.Value4 : Object.Blades

sub ObjectMain
	switch Object.State
	case 0
		Object.Frame=0
		break
	case 1
		Object.Frame=0
		Object.Value0=Object.Value2
		Object.Value0-=Object.XPos
		Object.Value0>>=5

		TempValue0=Object.Value2
		TempValue1=Object.XPos
		TempValue0>>=19
		TempValue1>>=19
		if TempValue0==TempValue1
			Object.State=2
			Object.Value3=0
		end if

		if Object.Value0>0
			Object.Direction=1
		else
			Object.Direction=0
		end if
		break
	case 2
		Object.Frame=0
		Object.Value3++
		if Object.Value3==15
			Object.State=3
		end if
		break
	case 3
		Object.Frame=2
		Object.Value3++

		if Object.Value3==90
			TempValue0=Object.Type
			TempValue0++
			CreateTempObject(TempValue0,0,Object.XPos,Object.YPos)
			if Object.Direction==1
				Object[TempObjectPos].XPos+=524288
			else
				Object[TempObjectPos].XPos-=524288
			end if

			Object[TempObjectPos].YPos+=1179648

			Object[TempObjectPos].XVelocity=Player.XPos
			Object[TempObjectPos].XVelocity-=Object[TempObjectPos].XPos
			Object[TempObjectPos].XVelocity/=48

			Object[TempObjectPos].YVelocity=Player.YPos
			Object[TempObjectPos].YVelocity-=Object[TempObjectPos].YPos
			Object[TempObjectPos].YVelocity/=48

			if Object[TempObjectPos].YVelocity<0
				FlipSign(Object[TempObjectPos].YVelocity)
			end if
		end if

		if Object.Value3==120
			Object.State=0
		end if
		break
	end switch

	TempValue0=Oscillation
	TempValue0<<=2
	Sin(Object.Value1,TempValue0)

	Object.Value1<<=6

	Object.XPos+=Object.Value0
	Object.YPos+=Object.Value1
end sub

sub ObjectPlayerInteraction
	if Object.State<5
		ArrayPos0=Player.EntityNo
		ArrayPos0+=2
		if Object[ArrayPos0].Type==TypeName[Ring Sparkle]
			PlayerObjectCollision(C_TOUCH,-24,-24,24,24)
		else
			PlayerObjectCollision(C_TOUCH,-15,-10,15,10)
		endif
		if CheckResult==1
			CallFunction(PlayerObject_BadnikBreak)
		endif
	endif
endsub

sub ObjectDraw
	Object.Frame+=Object.Direction
	DrawSprite(Object.Frame)

	TempValue0=Object.Value4
	TempValue0>>=1
	TempValue0+=4
	DrawSprite(TempValue0)
	Object.Value4++
	if Object.Value4>3
		Object.Value4=0
	end if
end sub

sub ObjectStartup
	LoadSpriteSheet("SSZ/Objects.gif")
	
	//HBomber Moving Frames
	SpriteFrame(-24,-10,47,19,52,1)
	SpriteFrame(-24,-10,47,19,52,21)

	//HBomber Shooting Frames
	SpriteFrame(-24,-10,30,29,52,59)
	SpriteFrame(-7,-10,30,29,67,89)

	//Heli-blades
	SpriteFrame(-16,-18,32,8,52,41)
	SpriteFrame(-16,-18,32,8,52,50)
end sub

sub RSDK
	LoadSpriteSheet("SSZ/Objects.gif")
	SetEditorIcon(Icon0,SingleIcon,-24,-10,47,19,52,1)
end sub