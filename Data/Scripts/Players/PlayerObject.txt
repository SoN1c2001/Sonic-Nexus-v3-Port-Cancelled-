//------------Sonic CD Player Object Script-------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

//-------Aliases-------//
#alias 1: TYPE_PLAYEROBJECT

#alias Player.Value0:Player.Rings
#alias Player.Value1:Player.AbilityTimer
#alias Player.Value2:Player.CustomAnimSpeed
#alias Player.Value3:Player.SpeedShoes
#alias Player.Value4:Player.Invincible
#alias Player.Value5:Player.BlinkTimer
#alias Player.Value6:Player.MinRollSpeed
#alias Player.Value7:Player.AnimationCheck
#alias Player.Value8:Player.ScrollDelay
#alias Player.Value9:Player.CameraOffset
#alias Player.Value10:Player.FlightFunction
#alias Player.Value11:Player.PeeloutFunction
#alias Player.Value12:Player.SpindashFunction
#alias Player.Value13:Player.Shield // ;)
#alias Player.Value14:Player.StoredXPos //Replacement of TempValue7 for Knux to avoid glitches
#alias Player.Value15:Player.FlightGravity

// Function declarations
#function PlayerObject_BadnikBreak
#function PlayerObject_Hit
#function PlayerObject_Kill
#function PlayerObject_ProcessPlayer
#function PlayerObject_Blank
#function PlayerObject_HandleGroundRot
#function PlayerObject_HandleAirRot
#function PlayerObject_RollAnimSpeed
#function PlayerObject_WalkAnimSpeed
#function PlayerObject_RunAnimSpeed
#function PlayerObject_HandleMovement
#function PlayerObject_HandleAirSpeed
#function PlayerObject_HandleRollDecel
#function PlayerObject_HandleAirMovement
#function PlayerObject_ResetOnFloor
#function PlayerObject_StartJump
#function PlayerObject_StartSpindashS2
#function PlayerObject_StartSpindashCD
#function PlayerObject_StartPeeloutS2
#function PlayerObject_StartPeeloutCD 
#function PlayerObject_StartTailsFlight
#function PlayerObject_StartKnuxGlide
#function PlayerObject_HandleGround
#function PlayerObject_HandleAir
#function PlayerObject_HandleRolling
#function PlayerObject_AirControlLock
#function PlayerObject_LookingUp
#function PlayerObject_Crouching
#function PlayerObject_HandleSpindashS2
#function PlayerObject_HandleSpindashCD
#function PlayerObject_HandlePeeloutS2
#function PlayerObject_HandlePeeloutCD
#function PlayerObject_HandleTailsFlight
#function PlayerObject_HandleKnuxGlide
#function PlayerObject_HandleKnuxClimb
#function PlayerObject_HandleKnuxClimbUp
#function PlayerObject_Hurt
#function PlayerObject_Knockback
#function PlayerObject_OuttaHere
#function PlayerObject_Death
#function PlayerObject_Drown
#function PlayerObject_CheckTile
#function PlayerObject_CorkscrewRun
#function PlayerObject_CorkscrewRoll
#function PlayerObject_KnucklesGlideDrop
#function PlayerObject_KnucklesGlideStop
#function PlayerObject_RollingJump
#function PlayerObject_Tunnel
#function PlayerObject_StartTTCtrlLock
#function PlayerObject_SpinningTop
#function PlayerObject_Hugged
#function PlayerObject_Handle3DRampAnims
#function PlayerObject_DebugMode
#function PlayerObject_SizeChange
#function PlayerObject_VictoryAni
#function PlayerObject_CheckShield
#function PlayerObject_AbilityLock
#function PlayerObject_BubbleBounce

#alias SaveRAM[67]:Options.Spindash
#alias SaveRAM[275]:Options.WalkAnim
#alias SaveRAM[276]:Options.RunAnim
#alias SaveRAM[278]:Options.VictoryAnim
#alias SaveRAM[279]:Options.GenBehaviour

function PlayerObject_BadnikBreak
	CheckEqual(Player.Animation,ANI_JUMPING)
	TempValue0=CheckResult
	CheckEqual(Player.Animation,ANI_SPINDASH)
	TempValue0|=CheckResult
	CheckEqual(Player.Animation,ANI_GLIDING)
	TempValue0|=CheckResult
	CheckEqual(Player.Animation,ANI_GLIDING_STOP)
	TempValue0|=CheckResult
	ArrayPos0=Player.EntityNo
	ArrayPos0+=2
	CheckEqual(Object[ArrayPos0].Type,TypeName[Invincibility])
	TempValue0|=CheckResult
	if Warp.Timer>0
		TempValue0|=1
	endif
	if Player.Animation==ANI_FLYING
		CheckGreater(Player.YPos,Object.YPos)
		TempValue0|=CheckResult
	endif
	if TempValue0==1
		ResetObjectEntity(Object.EntityNo,Flower_TypeNo,0,Object.XPos,Object.YPos)
		Object.DrawOrder=4
		CreateTempObject(TypeName[SmokePuff],0,Object.XPos,Object.YPos)
		Object[TempObjectPos].DrawOrder=4
		CreateTempObject(TypeName[ObjectScore],Object[26].Value0,Object.XPos,Object.YPos)
		Object[TempObjectPos].DrawOrder=4
		PlaySfx(8,0)
		if Player.YVelocity>0
			FlipSign(Player.YVelocity)
		else
			Player.YVelocity+=49152
		endif
		switch Object[26].Value0
		case 0
			Player.Score+=100
			break
		case 1
			Player.Score+=200
			break
		case 2
			Player.Score+=500
			break
		case 3
			Player.Score+=1000
			break

		endswitch
#platform: Use_Haptics
		HapticEffect(16,0,0,0)
#endplatform
	else
		if Player.Invincible==0
			Player.State=PlayerObject_Hurt
			if Player.XPos>Object.XPos
				Player.Speed=131072
			else
				Player.Speed=-131072
			endif
		endif
	endif
endfunction


function PlayerObject_Hit
	ArrayPos0=Player.EntityNo
	ArrayPos0+=2
	if Object[ArrayPos0].Type!=TypeName[Invincibility]
		if Player.Invincible==0
			Player.State=PlayerObject_Hurt
			if Player.XPos>Object.XPos
				Player.Speed=131072
			else
				Player.Speed=-131072
			endif
		endif
	endif
endfunction


function PlayerObject_Kill
	PlaySfx(5,0)
	Player.DrawOrder=5
	Player.Speed=0
	Player.XVelocity=0
	Player.YVelocity=-425984
	Player.State=PlayerObject_Death
	Player.Animation=ANI_DYING
	Player.TileCollisions=0
	Player.ObjectInteraction=0
	Screen.CameraEnabled=0
#platform: Use_Haptics
	HapticEffect(28,0,0,0)
#endplatform
endfunction

function PlayerObject_ProcessPlayer
#platform: Standard
	if Options.AttractMode==0
		if Player.ControlMode==0
			if Object[9].Type==TypeName[BlankObject]
				if KeyPress.Start==1
					KeyPress.Start=0
					if Options.DevMenuFlag==1
						Stage.State=STAGE_PAUSED
						PauseMusic()
						PlaySfx(27,0)
						StopSfx(24)
						StopSfx(25)
						Object[9].Type=TypeName[PauseMenu]
						Object[9].DrawOrder=6
						Object[9].Priority=2
					else
						EngineCallback(13)
					endif
				endif
			endif
		endif
		ProcessPlayerControl()
	endif
#endplatform

#platform: Mobile
	if Options.AttractMode==false
		if Options.TouchControls==true
			if Player.ControlMode==0
				CheckTouchRect(0,96,Screen.CenterX,Screen.YSize)
				if CheckResult>-1
					ArrayPos0=CheckResult
					TempValue0=TouchScreen[ArrayPos0].XPos
					TempValue0-=Options.DPadX
					TempValue1=TouchScreen[ArrayPos0].YPos
					TempValue1-=192
					ATan2(TempValue2,TempValue0,TempValue1)
					TempValue2+=32
					TempValue2&=255
					TempValue2>>=6
					switch TempValue2
					case 0
						KeyDown.Right=1
						break
					case 1
						KeyDown.Down=1
						break
					case 2
						KeyDown.Left=1
						break
					case 3
						KeyDown.Up=1
						break
					endswitch
				endif
				CheckTouchRect(Screen.CenterX,96,Screen.XSize,240)
				if CheckResult>-1
					KeyDown.ButtonA=1
				endif
				if Object[25].Value7==0
					KeyPress.ButtonA|=KeyDown.ButtonA
				endif
				Object[25].Value7=KeyDown.ButtonA
				if Object[9].Type==TypeName[BlankObject]
					CheckTouchRect(240,0,Screen.XSize,40)
					if CheckResult>-1
						Stage.State=STAGE_PAUSED
						PauseMusic()
						PlaySfx(27,0)
						StopSfx(24)
						StopSfx(25)
						Object[9].Type=TypeName[PauseMenu]
						Object[9].DrawOrder=6
						Object[9].Priority=2
						if Engine.FrameSkipTimer>-1
							Engine.FrameSkipTimer=-1
						endif
					endif
					if Engine.Message==2
						Stage.State=STAGE_PAUSED
						PauseMusic()
						PlaySfx(27,0)
						StopSfx(24)
						StopSfx(25)
						Object[9].Type=TypeName[PauseMenu]
						Object[9].DrawOrder=6
						Object[9].Priority=2
						if Engine.FrameSkipTimer>-1
							Engine.FrameSkipTimer=-1
						endif
					endif
				endif
			endif
		else
			if Player.ControlMode==0
				if Object[9].Type==TypeName[BlankObject]
					if KeyPress.Start==1
						KeyPress.Start=0
						Stage.State=STAGE_PAUSED
						PauseMusic()
						PlaySfx(27,0)
						StopSfx(24)
						StopSfx(25)
						Object[9].Type=TypeName[PauseMenu]
						Object[9].DrawOrder=6
						Object[9].Priority=2
						if Engine.FrameSkipTimer>-1
							Engine.FrameSkipTimer=-1
						endif
					endif
					if Engine.Message==2
						Stage.State=STAGE_PAUSED
						PauseMusic()
						PlaySfx(27,0)
						StopSfx(24)
						StopSfx(25)
						Object[9].Type=TypeName[PauseMenu]
						Object[9].DrawOrder=6
						Object[9].Priority=2
						if Engine.FrameSkipTimer>-1
							Engine.FrameSkipTimer=-1
						endif
					endif
				endif
			endif
		endif
		ProcessPlayerControl()
	endif
#endplatform

	if Player.SpeedShoes>0
		Player.SpeedShoes--
		if Player.SpeedShoes==0
			Player.Acceleration=3072
			Player.AirAcceleration=6144
			Player.TopSpeed=393216
			if Music.CurrentTrack==3
				PlayMusic(0)
			endif
		endif
	endif
	if Player.Invincible>0
		if Player.State!=PlayerObject_Knockback
			if Player.Invincible>2000
				Player.Invincible=120
				Player.BlinkTimer=3
			endif
		endif
		if Player.BlinkTimer>0
			Player.BlinkTimer++
			if Player.BlinkTimer>8
				Player.BlinkTimer=1
			endif
			if Player.BlinkTimer>4
				Player.Visible=0
			else
				Player.Visible=1
			endif
		endif
		Player.Invincible--
		if Player.Invincible==0
			Player.BlinkTimer=0
			Player.Visible=1
			if Music.CurrentTrack==2
				PlayMusic(0)
			endif
			if Object[+2].Type==TypeName[Invincibility]
				switch Object[+2].PropertyValue
				case 0
					TempValue0=Player.EntityNo
					TempValue0+=2
					ResetObjectEntity(TempValue0,TypeName[BlankObject],0,0,0)
					break
				case 1
					TempValue0=Player.EntityNo
					TempValue0+=2
					ResetObjectEntity(TempValue0,TypeName[BlankObject],0,0,0)
					Object[+2].Type=TypeName[BlueShield]
					Object[+2].PropertyValue=1
					Object[+2].Priority=1
					Object[+2].DrawOrder=4
					Object[+2].InkEffect=2
					Object[+2].Alpha=160
					Object[+2].XPos=Player.XPos
					Object[+2].YPos=Player.YPos
					break

				endswitch
			endif
		endif
	endif
	if Player.State!=PlayerObject_LookingUp
		if Player.State!=PlayerObject_Crouching
			if Player.LookPos>0
				Player.LookPos-=2
			endif
			if Player.LookPos<0
				Player.LookPos+=2
			endif
		endif
	endif
	if Warp.Timer>0
		Warp.Timer++
		if Warp.Timer==Warp.MaxTimer
			Screen.CameraEnabled=0
			CreateTempObject(TypeName[TimeWarp],0,0,0)
			Object[TempObjectPos].DrawOrder=6
		endif
	endif
	if Player.ScrollDelay>0
		Player.ScrollDelay--
		if Player.ScrollDelay==0
			Screen.CameraStyle=0
		endif
	endif
	if Stage.PlayerListPos!=2
		if Player.State!=PlayerObject_HandleTailsFlight
			if Player.FlightGravity!=0
				StopSfx(24)
				StopSfx(25)
				Player.FlightGravity=0
			endif
		endif
	endif
endfunction


function PlayerObject_Blank
	CheckResult=0
endfunction


function PlayerObject_HandleGroundRot
	CheckLower(Player.Angle, 5)
	TempValue0 = CheckResult
	CheckGreater(Player.Angle, 251)
	TempValue0 |= CheckResult
	
	if TempValue0 == true
		CheckLower(Player.Angle, 17)
		TempValue0 = CheckResult
		CheckGreater(Player.Angle, 231)
		TempValue0 |= CheckResult
		
		if TempValue0 == true
			Player.Rotation = 0
		else
			Player.Rotation = Player.Angle
			Player.Rotation *= 2
		end if
	else //We're in the rotate zone now boys	
		TempValue0 = Player.Speed
		if TempValue0 < 0
			FlipSign(TempValue0)
		end if
		
		CheckLower(TempValue0, 393217) //<= 0x60000
		TempValue1 = CheckResult
		TempValue1++
		
		//if (Angle <= 16 || Angle >= 232)
		CheckLower(Player.Angle, 17)
		TempValue2 = CheckResult
		CheckGreater(Player.Angle, 231)
		TempValue2 |= CheckResult
		
		//RotationBuffer
		if TempValue2 == true
			TempValue2 = 0
		else
			TempValue2 = Player.Angle
			TempValue2 *= 2
		end if

		//Rotation
		TempValue3 = Player.Rotation
		
		//RotationDifference
		TempValue4 = TempValue2
		TempValue4 -= TempValue3
		
		//RotationBuffer2
		if TempValue4 >= 0
			TempValue5 = TempValue2
			TempValue5 -= TempValue3
		else
			TempValue5 = TempValue3
			TempValue5 -= TempValue2
		end if
		
		//Angle
		ArrayPos0 = TempValue4
		ArrayPos0 += 512
		
		//RotationBuffer4
		if ArrayPos0 >= 0
			TempValue6 = TempValue4
			TempValue6 += 512
		else
			TempValue6 = TempValue3
			TempValue6 -= TempValue2
			TempValue6 -= 512
		end if
		
		//RotationBuffer3
		TempValue7 = TempValue3
		TempValue7 -= TempValue2
		TempValue7 -= 512
		
		if TempValue7 < 0
			TempValue7 = TempValue3
			TempValue7 -= TempValue2
			TempValue7 += 512
		end if
		
		TempValue0 = true
		if TempValue5 >= TempValue7
			if TempValue7 < TempValue6
				ArrayPos0 = TempValue4
				ArrayPos0 -= 512
			end if
		else
			if TempValue5 < TempValue6)
				TempValue0 = TempValue4
				TempValue0 >>= TempValue1
				Player.Rotation = TempValue3
				Player.Rotation += TempValue0
				Player.Rotation &= 511
				TempValue0 = false
			end if
		end if
		
		if TempValue0 == true
			//Finalise
			TempValue0 = ArrayPos0
			TempValue0 >>= TempValue1
			Player.Rotation = TempValue3
			Player.Rotation += TempValue0
			Player.Rotation &= 511
		end if
	end if
endfunction

function PlayerObject_HandleAirRot
	if Player.Rotation < 256
		if Player.Rotation > 0
			Player.Rotation -= 4
		else
			Player.Rotation = 0
		end if
	else
		if Player.Rotation < 512
			Player.Rotation += 4
		else
			Player.Rotation = 0
		end if
	end if
endfunction

function PlayerObject_RollAnimSpeed
#platform: Mobile
	if Stage.PlayerListPos==1
		Player.CustomAnimSpeed=120
	else
		Player.CustomAnimSpeed=Player.Speed
		if Player.CustomAnimSpeed<0
			FlipSign(Player.CustomAnimSpeed)
		endif
		Player.CustomAnimSpeed*=240
		Player.CustomAnimSpeed/=393216
		Player.CustomAnimSpeed+=48
	endif
#endplatform					
#platform: Standard
	Player.CustomAnimSpeed=Player.Speed
	if Player.CustomAnimSpeed<0
		FlipSign(Player.CustomAnimSpeed)
	endif
	Player.CustomAnimSpeed*=240
	Player.CustomAnimSpeed/=393216
	Player.CustomAnimSpeed+=48
#endplatform
endfunction


function PlayerObject_WalkAnimSpeed
	if Stage.PlayerListPos==0
		if Options.WalkAnim==0
			Player.AnimationSpeed=Player.Speed
			if Player.AnimationSpeed<0
				FlipSign(Player.AnimationSpeed)
			endif
			Player.AnimationSpeed*=40
			Player.AnimationSpeed/=196608
			Player.AnimationSpeed+=20
		else
			Player.AnimationSpeed=Player.Speed
			if Player.AnimationSpeed<0
				FlipSign(Player.AnimationSpeed)
			endif
			Player.AnimationSpeed*=60
			Player.AnimationSpeed/=393216
			Player.AnimationSpeed+=20
		endif
	else
		Player.AnimationSpeed=Player.Speed
		if Player.AnimationSpeed<0
			FlipSign(Player.AnimationSpeed)
		endif
		Player.AnimationSpeed*=60
		Player.AnimationSpeed/=393216
		Player.AnimationSpeed+=20
	endif
endfunction


function PlayerObject_RunAnimSpeed
	if Stage.PlayerListPos==0
		if Options.RunAnim==0
			Player.AnimationSpeed=Player.Speed
			if Player.AnimationSpeed<0
				FlipSign(Player.AnimationSpeed)
			endif
			Player.AnimationSpeed*=80
			Player.AnimationSpeed/=393216
		else
			Player.AnimationSpeed=Player.Speed
			if Player.AnimationSpeed<0
				FlipSign(Player.AnimationSpeed)
			endif
			Player.AnimationSpeed*=100
			Player.AnimationSpeed/=393216
		endif
	else
		Player.AnimationSpeed=Player.Speed
		if Player.AnimationSpeed<0
			FlipSign(Player.AnimationSpeed)
		endif
		Player.AnimationSpeed*=80
		Player.AnimationSpeed/=393216
	endif
endfunction


function PlayerObject_HandleMovement
	if Player.ControlLock>0
		Player.ControlLock--
		Sin256(TempValue0,Player.Angle)
		TempValue0*=8192
		TempValue0>>=8
		Player.Speed+=TempValue0
	else
		if Player.Left==1
			TempValue0=Player.TopSpeed
			FlipSign(TempValue0)
			if Player.Speed>TempValue0
				if Player.Speed>0
					if Player.CollisionMode==0
						if Player.Speed>262144
							Player.Skidding=16
						endif
					endif
					if Player.Speed<32768
						Player.Speed=-32768
						Player.Skidding=0
					else
						Player.Speed-=32768
					endif
				else
					Player.Speed-=Player.Acceleration
					Player.Skidding=0
				endif
			endif
			if Player.Speed<=0
				Player.Direction=FACING_LEFT
			endif
		endif
		if Player.Right==1
			if Player.Speed<Player.TopSpeed
				if Player.Speed<0
					if Player.CollisionMode==0
						if Player.Speed<-262144
							Player.Skidding=16
						endif
					endif
					if Player.Speed>-32768
						Player.Speed=32768
						Player.Skidding=0
					else
						Player.Speed+=32768
					endif
				else
					Player.Speed+=Player.Acceleration
					Player.Skidding=0
				endif
			endif
			if Player.Speed>=0
				Player.Direction=FACING_RIGHT
			endif
		endif
		TempValue0=Player.Left
		TempValue0|=Player.Right
		if TempValue0==0
			if Player.Speed>0
				Player.Speed-=Player.Deceleration
				if Player.Speed<0
					Player.Speed=0
				endif
			else
				Player.Speed+=Player.Deceleration
				if Player.Speed>0
					Player.Speed=0
				endif
			endif
			if Player.Speed>8192
				Sin256(TempValue0,Player.Angle)
				TempValue0*=8192
				TempValue0>>=8
				Player.Speed+=TempValue0
			endif
			if Player.Speed<-8192
				Sin256(TempValue0,Player.Angle)
				TempValue0*=8192
				TempValue0>>=8
				Player.Speed+=TempValue0
			endif
			if Player.Angle>192
				if Player.Angle<228
					if Player.Speed>-65536
						if Player.Speed<65536
							Player.ControlLock=30
						endif
					endif
				endif
			endif
			if Player.Angle>28
				if Player.Angle<64
					if Player.Speed>-65536
						if Player.Speed<65536
							Player.ControlLock=30
						endif
					endif
				endif
			endif
		else
			Sin256(TempValue0,Player.Angle)
			TempValue0*=8192
			TempValue0>>=8
			Player.Speed+=TempValue0
			if Player.Right==1
				if Player.Left==0
					if Player.Angle>192
						if Player.Angle<228
							if Player.Speed<163840
								if Player.Speed>-131072
									Player.ControlLock=30
								endif
							endif
						endif
					endif
				endif
			else
				if Player.Left==1
					if Player.Angle>28
						if Player.Angle<64
							if Player.Speed>-163840
								if Player.Speed<131072
									Player.ControlLock=30
								endif
							endif
						endif
					endif
				endif
			endif
		endif
	endif
endfunction


function PlayerObject_HandleAirSpeed
	if Player.YVelocity>-262144
		if Player.YVelocity<0
			TempValue0=Player.Speed
			TempValue0>>=5
			Player.Speed-=TempValue0
		endif
	endif
	TempValue0=Player.TopSpeed
	FlipSign(TempValue0)
	if Player.Speed>TempValue0
		if Player.Left==1
			Player.Speed-=Player.AirAcceleration
			Player.Direction=FACING_LEFT
		endif
	else
		if Player.Left==1
			Player.Direction=FACING_LEFT
		endif
	endif
	if Player.Speed<Player.TopSpeed
		if Player.Right==1
			Player.Speed+=Player.AirAcceleration
			Player.Direction=FACING_RIGHT
		endif
	else
		if Player.Right==1
			Player.Direction=FACING_RIGHT
		endif
	endif
	if Options.OriginalControls==1
#platform: Standard
		if Player.Left==1
			if Player.Speed<-393216
				Player.Speed=-393216
			endif
		endif
		if Player.Right==1
			if Player.Speed>393216
				Player.Speed=393216
			endif
		endif
#endplatform
#platform: Mobile
		if Player.Left==1
			TempValue0=Player.TopSpeed
			FlipSign(TempValue0)
			if Player.Speed<TempValue0
				Player.Speed=TempValue0
			endif
		endif
		if Player.Right==1
			if Player.Speed>Player.TopSpeed
				Player.Speed=Player.TopSpeed
			endif
		endif
#endplatform
	endif
endfunction


function PlayerObject_HandleRollDecel
	if Player.Right==1
		if Player.Speed<0
			Player.Speed+=Player.RollingDeceleration
		endif
	endif
	if Player.Left==1
		if Player.Speed>0
			Player.Speed-=Player.RollingDeceleration
		endif
	endif
	if Player.Speed>0
		Player.Speed-=Player.AirDeceleration
		if Player.Speed<0
			Player.Speed=0
		endif
		if Player.Speed==0
			if Player.Angle>224
				Player.State=PlayerObject_HandleGround
			endif
			if Player.Angle<32
				Player.State=PlayerObject_HandleGround
			endif
		endif
		Sin256(TempValue0,Player.Angle)
		if TempValue0>0
			Sin256(TempValue0,Player.Angle)
			TempValue0*=20480
		else
			Sin256(TempValue0,Player.Angle)
			TempValue0*=7680
		endif
		TempValue0>>=8
		Player.Speed+=TempValue0
	else
		Player.Speed+=Player.AirDeceleration
		if Player.Speed>0
			Player.Speed=0
		endif
		if Player.Speed==0
			if Player.Angle>224
				Player.State=PlayerObject_HandleGround
			endif
			if Player.Angle<32
				Player.State=PlayerObject_HandleGround
			endif
		endif
		Sin256(TempValue0,Player.Angle)
		if TempValue0<0
			Sin256(TempValue0,Player.Angle)
			TempValue0*=20480
		else
			Sin256(TempValue0,Player.Angle)
			TempValue0*=7680
		endif
		TempValue0>>=8
		Player.Speed+=TempValue0
	endif
	if Player.Speed>1572864
		Player.Speed=1572864
	endif
	if Player.Speed<-1572864
		Player.Speed=-1572864
	endif
endfunction


function PlayerObject_HandleAirMovement
	Player.TrackScroll=1
	Player.YVelocity+=Player.GravityStrength
	if Player.YVelocity<Player.JumpCap
		if Player.JumpHold==0
			if Player.Timer>0
				Player.YVelocity=Player.JumpCap
				TempValue0=Player.Speed
				TempValue0>>=5
				Player.Speed-=TempValue0
			endif
		endif
	endif
	Player.XVelocity=Player.Speed
	CallFunction(PlayerObject_HandleAirRot)
	Player.CollisionMode=0
	if Player.Animation==ANI_JUMPING
		Player.AnimationSpeed=Player.CustomAnimSpeed
	endif
endfunction


function PlayerObject_ResetOnFloor
	Player.TrackScroll=0
	Cos256(TempValue0,Player.Angle)
	TempValue0*=Player.Speed
	TempValue0>>=8
	Player.XVelocity=TempValue0
	Sin256(TempValue0,Player.Angle)
	TempValue0*=Player.Speed
	TempValue0>>=8
	Player.YVelocity=TempValue0
endfunction


function PlayerObject_StartJump
	CheckResult=0
	if Player.CollisionMode==0
		TempValue6=Object.XPos
		TempValue7=Object.YPos
		Object.XPos=Player.XPos
		Object.YPos=Player.YPos
		TempValue0=Player.CollisionTop
		TempValue0-=2
		ObjectTileCollision(2,0,TempValue0,0)
		Object.XPos=TempValue6
		Object.YPos=TempValue7
	endif
	if CheckResult==0
		Player.ControlLock=0
		Player.Gravity=1
		Player.AbilityTimer=8
		Sin256(Player.XVelocity,Player.Angle)
		Player.XVelocity*=Player.JumpStrength
		Cos256(TempValue0,Player.Angle)
		TempValue0*=Player.Speed
		Player.XVelocity+=TempValue0
		Player.XVelocity>>=8
		Sin256(Player.YVelocity,Player.Angle)
		Player.YVelocity*=Player.Speed
		Cos256(TempValue0,Player.Angle)
		TempValue0*=Player.JumpStrength
		Player.YVelocity-=TempValue0
		Player.YVelocity>>=8
		Player.Speed=Player.XVelocity
		Player.TrackScroll=1
		Player.Animation=ANI_JUMPING
		Player.Angle=0
		Player.CollisionMode=0
		Player.Timer=1
		CallFunction(PlayerObject_RollAnimSpeed)
		Player.Value14 = 0
		if Player.State == PlayerObject_HandleRolling
			if Player.Direction==FACING_LEFT
				Player.Value14 = 2
			endif
			if Player.Direction==FACING_RIGHT
				Player.Value14 = 1
			endif
		end if
			Player.State=PlayerObject_HandleAir
		PlaySfx(0,0)
	endif
endfunction


function PlayerObject_StartSpindashS2
	Player.State=PlayerObject_HandleSpindashS2
	Player.Animation=ANI_SPINDASH
	Player.AbilityTimer=0
	PlaySfx(6,0)
	CreateTempObject(TypeName[DustPuff],Object.EntityNo,Player.XPos,Player.YPos)
	Object[TempObjectPos].iYPos=Player.CollisionBottom
	Object[TempObjectPos].YPos+=Player.YPos
	Object[TempObjectPos].Frame=4
	Object[TempObjectPos].DrawOrder=4
	Object[TempObjectPos].Direction=Player.Direction
#platform: Use_Haptics
	HapticEffect(112,0,0,0)
#endplatform
endfunction


function PlayerObject_StartSpindashCD
	Player.State=PlayerObject_HandleSpindashCD
	Player.Animation=ANI_JUMPING
	Player.YPos+=327680
	Player.AbilityTimer=0
	PlaySfx(6,0)
#platform: Use_Haptics
	HapticEffect(112,0,0,0)
#endplatform
endfunction


function PlayerObject_StartPeeloutS2
	Player.State=PlayerObject_HandlePeeloutS2
	Player.AbilityTimer=0
	PlaySfx(6,0)
#platform: Use_Haptics
	HapticEffect(115,0,0,0)
#endplatform
endfunction


function PlayerObject_StartPeeloutCD
	Player.State=PlayerObject_HandlePeeloutCD
	Player.AbilityTimer=0
	PlaySfx(6,0)
#platform: Use_Haptics
	HapticEffect(115,0,0,0)
#endplatform
endfunction


function PlayerObject_StartTailsFlight
	if Player.AbilityTimer>0
		Player.AbilityTimer--
	else
		if Player.JumpPress==1
			Player.Timer=0
			Player.State=PlayerObject_HandleTailsFlight
			Player.FlightGravity=2048
			if Player.GravityStrength==14336
				PlaySfx(24,1)
				Player.Animation=ANI_FLYING
			else
				Player.Animation=ANI_SWIMMING
			endif
		endif
	endif
endfunction

function PlayerObject_StartKnuxGlide
	if Player.JumpPress==1
		Player.Speed=262144
		if Options.GenBehaviour == true
			Player.YVelocity+=131072
		end if
		if Player.YVelocity<0
			Player.YVelocity=0
		endif
		Player.State=PlayerObject_HandleKnuxGlide
		if Player.Direction==FACING_RIGHT
			Player.Timer=0
			Player.Rotation=0
			Player.XVelocity=262144
		else
			Player.Timer=256
			Player.Rotation=1
			Player.XVelocity=-262144
		endif
		Player.Animation=ANI_GLIDING
		Player.Frame=2
	endif
endfunction

function PlayerObject_HandleKnuxGlide
	if Player.Gravity==1
		if Player.JumpHold==1
			if Player.Left==1
				Player.Rotation=1
			endif
			if Player.Right==1
				Player.Rotation=0
			endif	
			
			if Player.Rotation!=0
				if Player.Timer<256
					Player.Timer+=4
				endif
				if Player.Timer>256
					Player.Timer=256
				endif
			else
				if Player.Timer>0
					Player.Timer-=4
				endif		
				if Player.Timer<0
					Player.Timer=0
				endif
			endif

			CheckEqual(Player.Timer,256)
			TempValue0=CheckResult
			CheckEqual(Player.Timer,0)
			TempValue0|=CheckResult
			if TempValue0==1
				if Player.Speed<1572864
					Player.Speed+=1024
				endif
			else
				if Player.Speed<262144
					Player.Speed+=4096
				endif
			endif

			if Player.YVelocity>32768
				Player.YVelocity-=8192
			else
				Player.YVelocity+=8192
			endif

			if Player.Timer<170
				if Player.Timer>86
					Player.Frame=0
				else
					if Player.Timer>44
						Player.Frame=1
					else
						Player.Frame=2
					endif
				endif
			else
				if Player.Timer<212
					Player.Frame=1
				else
					Player.Frame=2
				endif
			endif

			if Player.Animation==ANI_JUMPING
				Player.state=PlayerObject_HandleAir
			endif

			Player.StoredXPos=Player.XPos
			Object.XPos=Player.StoredXPos
			Player.XPos+=Player.XVelocity
			Object.YPos=Player.YPos
			if Player.Timer<128
				Player.Direction=FACING_RIGHT
				ObjectTileGrip(1,-5,-2,Player.CollisionPlane)
				TempValue0=CheckResult
			else
				Player.Direction=FACING_LEFT	
				ObjectTileGrip(2,4,-2,Player.CollisionPlane)
				TempValue0=CheckResult
			endif
			TempValue2=Object.XPos
			Player.XPos=Player.StoredXPos
			Player.XPos+=Player.XVelocity
			if Player.Timer<128
				Player.Direction=FACING_RIGHT
				ObjectTileGrip(1,-5,11,Player.CollisionPlane)
				TempValue1=CheckResult
			else
				Player.Direction=FACING_LEFT
				ObjectTileGrip(2,4,11,Player.CollisionPlane)
				TempValue1=CheckResult		
			endif
			TempValue3=Object.XPos
			Cos(Player.XVelocity,Player.Timer)
			Player.XVelocity*=Player.Speed
			Player.XVelocity>>=9	

			if Player.Direction==FACING_RIGHT
				if Warp.Destination>0
					if Player.XVelocity<262144
						if Warp.Timer>99
							if Warp.Timer<Warp.MaxTimer
								Warp.Destination=0
							endif
						endif
						Warp.Timer=0
					endif
				endif
			else
				if Warp.Destination>0
					if Player.XVelocity>-262144
						if Warp.Timer>99
							if Warp.Timer<Warp.MaxTimer
								Warp.Destination=0
							endif
						endif
						Warp.Timer=0
					endif
				endif
			endif

			Player.XPos=Player.StoredXPos
			CheckResult=TempValue0
			CheckResult&=TempValue1
			if CheckResult==1
				if Player.Frame==2
					if TempValue2==TempValue3
						Player.State=PlayerObject_HandleKnuxClimb
						Player.Speed=0
						Player.XVelocity=0
						Player.YVelocity=0
						Player.Timer=0
						PlaySfx(SfxName[Catch],0)
					else
						Player.Timer=0
						Player.XVelocity>>=2
						Player.Speed=Player.XVelocity
						Player.Animation=ANI_GLIDING_DROP
						Player.State=PlayerObject_KnucklesGlideDrop
					endif
				endif
			else
				if TempValue0==1
					Player.Speed=0
					Player.Timer=0
					Player.XVelocity>>=2
					Player.Speed=Player.XVelocity
					Player.Animation=ANI_GLIDING_DROP
					Player.State=PlayerObject_KnucklesGlideDrop
				endif
			endif
		else
			Player.Timer=0
			Player.XVelocity>>=2
			Player.Speed=Player.XVelocity
			Player.Animation = ANI_GLIDING_DROP
			if Options.GenBehaviour == true
				if Player.ixpos <= 10
					if Player.Direction==FACING_LEFT
						Player.Direction = FACING_RIGHT
					end if
				end if
			end if
			Player.State=PlayerObject_KnucklesGlideDrop
		endif
	else
		if Player.CollisionMode==0
			Player.Timer=0
			Player.State=PlayerObject_KnucklesGlideStop
			Player.Animation=ANI_GLIDING_STOP
			Player.Speed=Player.XVelocity
		else
			Player.State=PlayerObject_HandleGround
			CallFunction(PlayerObject_ResetOnFloor)
			Player.Skidding=0
		endif
	endif
	
	TempValue0=Stage.YBoundary1
	TempValue0+=16
	TempValue0<<=16
	if Player.YPos<TempValue0
		Player.XVelocity=0
		Player.Speed=Player.XVelocity
	endif
endfunction

function PlayerObject_HandleKnuxClimb
	if Warp.Destination>0
		if Warp.Timer>99
			if Warp.Timer<Warp.MaxTimer
				Warp.Destination=0
			endif
		endif
		Warp.Timer=0
	endif
	if Player.Gravity==1
		if Mini_PlayerFlag==1
			if Player.Direction==FACING_LEFT
				Player.XPos-=131072
			endif
		endif
		Player.Animation=ANI_CLIMBING
		if Player.Up==1
			Player.YVelocity=-65536
			TempValue0=Player.CollisionTop
			TempValue0*=-65536
			if Player.YPos<TempValue0
				Player.YPos=TempValue0
			endif
			Player.Timer++
			if Player.Timer==4
				Player.Timer=0
				Player.Frame++
				Player.Frame%=6
			endif
		else
			if Player.Down==1
				Player.YVelocity=65536
				Player.Timer++
				if Player.Timer==4
					Player.Timer=0
					if Player.Frame<1
						Player.Frame+=6
					endif
					Player.Frame--
				endif
			else
				Player.YVelocity=0
			endif
		endif
		if Player.JumpPress==1
			Player.Animation=ANI_JUMPING
			Player.State=PlayerObject_HandleAir
			Player.Timer=0
			PlaySfx(0,0)
			if Player.Direction==FACING_LEFT
				Player.XVelocity=262144
				Player.Speed=262144
				Player.Direction=FACING_RIGHT
			else
				Player.XVelocity=-262144
				Player.Speed=-262144
				Player.Direction=FACING_LEFT
			endif
			Player.YVelocity=-262144
			if Player.GravityStrength!=14336
				Player.XVelocity>>=1
				Player.Speed>>=1
				Player.YVelocity>>=1
			endif
		else
			TempValue2=Player.YPos
			TempValue3=Player.YPos
			Object.YPos=TempValue2
			if Player.Direction==FACING_RIGHT
				ObjectTileGrip(1,10,-10,Player.CollisionPlane)
				TempValue0=CheckResult
				ObjectTileGrip(1,10,11,Player.CollisionPlane)
				TempValue1=CheckResult
				if Player.YPos>TempValue3
					Player.YPos=TempValue3
				endif
			else
				ObjectTileGrip(2,-10,-10,Player.CollisionPlane)
				TempValue0=CheckResult
				ObjectTileGrip(2,-10,11,Player.CollisionPlane)
				TempValue1=CheckResult
				if Player.YPos<TempValue3
					Player.YPos=TempValue3
				endif
			endif
			if TempValue0==0
				Player.YPos=TempValue2
				Player.TileCollisions=0
				Player.YVelocity=0
				Player.Timer=0
				CheckResult=0
				if Player.Direction==FACING_LEFT
					if Mini_PlayerFlag==1
						Player.XPos+=131072
					endif
				endif
				Player.Animation=ANI_LEDGEPULLUP
				if Mini_PlayerFlag==1
					Player.YPos-=720896
				endif
				Player.State=PlayerObject_HandleKnuxClimbUp 
			else
				if TempValue1==0
					Player.Animation=ANI_GLIDING_DROP
					Player.PrevAnimation=ANI_GLIDING_DROP
					Player.Frame=2
					Player.Timer=0
					Player.State=PlayerObject_KnucklesGlideDrop
				endif
			endif
		endif
	else
		if Stage.PlayerListPos==0
			if Options.WalkAnim==0
				Player.Animation=ANI_WALKING
			else
				Player.Animation=ANI_ALTWALK
			endif
		else
				Player.Animation=ANI_WALKING
		endif
		Player.State=PlayerObject_HandleGround
		CallFunction(PlayerObject_ResetOnFloor)
	endif
endfunction

function PlayerObject_HandleGround
	//Rotation
	if Player.Gravity == 0
		CallFunction(PlayerObject_HandleGroundRot)
	else
		CallFunction(PlayerObject_HandleAirRot)
	endif	
	if Player.Animation!=ANI_SKIDDING
		TempValue7=1
	else
		TempValue7=0
	endif
	CallFunction(PlayerObject_HandleMovement)
	if Player.Gravity==1
		Player.State=PlayerObject_HandleAir
		CallFunction(PlayerObject_HandleAirMovement)
	else
		CallFunction(PlayerObject_ResetOnFloor)
		if Player.Speed==0
			if Warp.Destination>0
				if Warp.Timer>99
					if Warp.Timer<Warp.MaxTimer
						Warp.Destination=0
					endif
				endif
				Warp.Timer=0
			endif
			if Player.CollisionMode==0
				if Player.Timer<240
					Player.Animation=ANI_STOPPED
					Player.Timer++
				else
					Player.Animation=ANI_WAITING
					if Stage.PlayerListPos==0
						Player.Timer++
						if Player.Timer==10620
							Player.Timer=0
							PlaySfx(26,0)
							Player.State=PlayerObject_OuttaHere
							Player.Animation=ANI_BORED
						endif
					endif
				endif
				if Player[1].Flailing==0
					if Player[2].Flailing==0
						Player.Timer=0
						if Player.Direction==FACING_LEFT
							Player.Animation=ANI_FLAILINGLEFT
						else
							Player.Animation=ANI_FLAILINGRIGHT
						endif
						if Stage.PlayerListPos==2
							Player.Direction=FACING_LEFT
						endif
					endif
					if Player[0].Flailing==0
						Player.Timer=0
						if Player.Direction==FACING_RIGHT
							Player.Animation=ANI_FLAILINGLEFT
						else
							Player.Animation=ANI_FLAILINGRIGHT
						endif
						if Stage.PlayerListPos==2
							Player.Direction=FACING_RIGHT
						endif
					endif
				endif
			endif
		else
			Player.Timer=0
			if Player.Speed>0
				if Player.Speed<390594
					if Stage.PlayerListPos==0
						if Options.WalkAnim==0
							Player.Animation=ANI_WALKING
						else
							Player.Animation=ANI_ALTWALK
						endif
					else
							Player.Animation=ANI_WALKING
					endif
					CallFunction(PlayerObject_WalkAnimSpeed)
					if Warp.Destination>0
						if Warp.Timer>99
							if Warp.Timer<Warp.MaxTimer
								Warp.Destination=0
							endif
						endif
						Warp.Timer=0
					endif
				else
					if Warp.Destination>0
						if Warp.Timer==0
							Warp.Timer=1
							ResetObjectEntity(3,TypeName[WarpStar],0,Player.XPos,Player.YPos)
							Object[3].Value0=7
							Object[3].DrawOrder=4
						endif
					endif
					if Player.Speed>655359
						Player.Animation=ANI_PEELOUT
					else
						if Stage.PlayerListPos==0
							if Options.RunAnim==0
								Player.Animation=ANI_RUNNING
							else
								Player.Animation=ANI_ALTRUN
							endif
						else
								Player.Animation=ANI_RUNNING
						endif
					endif
					CallFunction(PlayerObject_RunAnimSpeed)
				endif
			else
				if Player.Speed>-390594
					if Stage.PlayerListPos==0
						if Options.WalkAnim==0
							Player.Animation=ANI_WALKING
						else
							Player.Animation=ANI_ALTWALK
						endif
					else
							Player.Animation=ANI_WALKING
					endif
					CallFunction(PlayerObject_WalkAnimSpeed)
					if Warp.Destination>0
						if Warp.Timer>99
							if Warp.Timer<Warp.MaxTimer
								Warp.Destination=0
							endif
						endif
						Warp.Timer=0
						Object[3].Type=TypeName[BlankObject]
					endif
				else
					if Warp.Destination>0
						if Warp.Timer==0
							Warp.Timer=1
							ResetObjectEntity(3,TypeName[WarpStar],0,Player.XPos,Player.YPos)
							Object[3].Value0=7
							Object[3].DrawOrder=4
						endif
					endif
					if Player.Speed<-655359
						Player.Animation=ANI_PEELOUT
					else
						if Stage.PlayerListPos==0
							if Options.RunAnim==0
								Player.Animation=ANI_RUNNING
							else
								Player.Animation=ANI_ALTRUN
							endif
						else
								Player.Animation=ANI_RUNNING
						endif
					endif
					CallFunction(PlayerObject_RunAnimSpeed)
				endif
			endif
		endif
		if Player.Skidding>0
			if TempValue7==1
				PlaySfx(3,0)
			endif
			Player.Animation=ANI_SKIDDING
			Player.AnimationSpeed=0
			Player.Skidding--
			if Ring.AniCount==0
				CreateTempObject(TypeName[DustPuff],0,Player.XPos,Player.YPos)
				Object[TempObjectPos].iYPos+=Player.CollisionBottom
				Object[TempObjectPos].DrawOrder=Player.DrawOrder
			endif
			if Player.Speed>0
				Player.Direction=FACING_RIGHT
			else
				Player.Direction=FACING_LEFT
			endif
		endif
		if Player.CollisionMode==0
			if Player.Pushing==2
				Player.Animation=ANI_PUSHING
				Player.AnimationSpeed=0
			endif
		endif
		if Player.JumpPress==1
			CallFunction(PlayerObject_StartJump)
		else
			if Player.Up==1
				if Player.Speed==0
					if Player.Animation!=ANI_FLAILINGLEFT
						if Player.Animation!=ANI_FLAILINGRIGHT
							Player.State=PlayerObject_LookingUp
							Player.Animation=ANI_LOOKINGUP
							Player.Timer=0
						endif
					endif
				endif
			endif
			if Player.Down==1
				if Player.Speed==0
					if Player.Animation!=ANI_FLAILINGLEFT
						if Player.Animation!=ANI_FLAILINGRIGHT
							Player.State=PlayerObject_Crouching
							Player.Animation=ANI_LOOKINGDOWN
							Player.Timer=0
						endif
					endif
				else
					if Player.Left==0
						if Player.Right==0
							if Player.Speed>0
								if Player.Speed>34816
									PlaySfx(28,0)
									Player.State=PlayerObject_HandleRolling
									Player.Animation=ANI_JUMPING
									Player.iYPos-=Player.CameraOffset
									Player.AbilityTimer=1024
								endif
							else
								if Player.Speed<-34816
									PlaySfx(28,0)
									Player.State=PlayerObject_HandleRolling
									Player.Animation=ANI_JUMPING
									Player.iYPos-=Player.CameraOffset
									Player.AbilityTimer=1024
								endif
							endif
						endif
					endif
				endif
			endif
		endif
	endif
endfunction


function PlayerObject_HandleAir
	CallFunction(PlayerObject_HandleAirSpeed)
	if Player.Gravity==1
		CallFunction(PlayerObject_HandleAirMovement)
		if Warp.Destination>0
			TempValue0=Player.YVelocity
			if TempValue0<0
				FlipSign(TempValue0)
			endif
			if TempValue0<393216
				TempValue0=Player.XVelocity
				if TempValue0<0
					FlipSign(TempValue0)
				endif
				if TempValue0<393216
					TempValue0=Player.XVelocity
					TempValue0-=Warp.SpeedCompare
					if TempValue0<0
						FlipSign(TempValue0)
					endif
					if TempValue0>262144
						if Warp.Timer>99
							if Warp.Timer<Warp.MaxTimer
								Warp.Destination=0
							endif
						endif
						Warp.Timer=0
					endif
				endif
			endif
			Warp.SpeedCompare=Player.XVelocity
		endif
		if Player.YVelocity>131072
			if Player.Animation==ANI_FLAILINGLEFT
				if Stage.PlayerListPos==0
					if Options.WalkAnim==0
						Player.Animation=ANI_WALKING
					else
						Player.Animation=ANI_ALTWALK
					endif
				else
						Player.Animation=ANI_WALKING
				endif
			endif
			if Player.Animation==ANI_FLAILINGRIGHT
				if Stage.PlayerListPos==0
					if Options.WalkAnim==0
						Player.Animation=ANI_WALKING
					else
						Player.Animation=ANI_ALTWALK
					endif
				else
						Player.Animation=ANI_WALKING
				endif
			endif
		endif
		if Player.Animation==ANI_BOUNCING
			if Player.YVelocity>=0
				if Player.AnimationCheck==ANI_STOPPED
					if Stage.PlayerListPos==0
						if Options.WalkAnim==0
							Player.Animation=ANI_WALKING
						else
							Player.Animation=ANI_ALTWALK
						endif
					else
							Player.Animation=ANI_WALKING
					endif
				endif
				Player.Animation=Player.AnimationCheck
			endif
		endif
		if Player.Animation==ANI_HURT
			if Player.YVelocity>=0
				if Player.AnimationCheck==ANI_STOPPED
					if Stage.PlayerListPos==0
						if Options.WalkAnim==0
							Player.Animation=ANI_WALKING
						else
							Player.Animation=ANI_ALTWALK
						endif
					else
							Player.Animation=ANI_WALKING
					endif
				endif
				Player.Animation=Player.AnimationCheck
			endif
		endif
		if Player.Animation==ANI_JUMPING
			CallFunction(Player.FlightFunction)
		endif
	else
		Player.State=PlayerObject_HandleGround
		CallFunction(PlayerObject_ResetOnFloor)
		Player.Skidding=0
	endif
endfunction


function PlayerObject_HandleRolling
	//Rotation
	if Player.Gravity == 0
		CallFunction(PlayerObject_HandleGroundRot)
	else
		CallFunction(PlayerObject_HandleAirRot)
	end if
	CallFunction(PlayerObject_HandleRollDecel)
	if Player.Gravity==1
		Player.State=PlayerObject_HandleAir
		Player.Timer=0
		CallFunction(PlayerObject_HandleAirMovement)
	else
		CallFunction(PlayerObject_RollAnimSpeed)
		Player.AnimationSpeed=Player.CustomAnimSpeed
		TempValue0=Player.Speed
		if TempValue0<0
			FlipSign(TempValue0)
		endif
		if TempValue0<390594
			if Warp.Destination>0
				if Warp.Timer>99
					if Warp.Timer<Warp.MaxTimer
						Warp.Destination=0
					endif
				endif
				Warp.Timer=0
			endif
		else
			if Warp.Destination>0
				if Warp.Timer==0
					Warp.Timer=1
					ResetObjectEntity(3,TypeName[WarpStar],0,Player.XPos,Player.YPos)
					Object[3].Value0=7
					Object[3].DrawOrder=4
				endif
			endif
		endif
		CallFunction(PlayerObject_ResetOnFloor)
		if Player.JumpPress==1
			CallFunction(PlayerObject_StartJump)
		endif
	endif
endfunction


function PlayerObject_AirControlLock
	Player.Left=0
	Player.Right=0
	CallFunction(PlayerObject_HandleAirSpeed)
	if Player.Gravity==1
		CallFunction(PlayerObject_HandleAirMovement)
	else
		Player.State=PlayerObject_HandleGround
		CallFunction(PlayerObject_ResetOnFloor)
		Player.Skidding=0
	endif
endfunction


function PlayerObject_LookingUp
	if Player.Up==0
		Player.State=PlayerObject_HandleGround
		Player.Timer=0
	else
		if Player.Timer<60
			Player.Timer++
		else
			if Player.LookPos>-112
				Player.LookPos-=2
			endif
		endif
		if Player.Gravity==1
			Player.State=PlayerObject_HandleAir
			Player.Timer=0
		else
			if Player.JumpPress==1
				CallFunction(Player.PeeloutFunction)
			endif
		endif
	endif
endfunction


function PlayerObject_Crouching
	if Player.Down==0
		Player.State=PlayerObject_HandleGround
		Player.Timer=0
	else
		if Player.Timer<60
			Player.Timer++
		else
			if Player.LookPos<96
				Player.LookPos+=2
			endif
		endif
		if Player.Gravity==1
			Player.State=PlayerObject_HandleAir
			Player.Timer=0
		else
			if Player.JumpPress==1
				CallFunction(Player.SpindashFunction)
			endif
		endif
	endif
endfunction


function PlayerObject_HandleSpindashS2
	if Player.Gravity==1
		Player.State=PlayerObject_HandleAir
		Player.Speed=0
	endif
	if Player.JumpPress==1
		if Player.AbilityTimer<512
			Player.AbilityTimer+=64
		endif
		Player.Frame=0
		PlaySfx(6,0)
	else
		if Player.AbilityTimer>0
			Player.AbilityTimer--
		endif
	endif
	if Player.Down==0
		Player.Timer=0
		Player.State=PlayerObject_HandleRolling
		Player.Animation=ANI_JUMPING
		Player.iYPos-=Player.CameraOffset
		Player.ScrollDelay=15
		Screen.CameraStyle=4
		TempValue0=Player.AbilityTimer
		TempValue0<<=9
		TempValue0+=524288
		if Player.Direction==FACING_RIGHT
			Player.Speed=TempValue0
		else
			Player.Speed=TempValue0
			FlipSign(Player.Speed)
		endif
		PlaySfx(7,0)
		CallFunction(PlayerObject_ResetOnFloor)
#platform: Use_Haptics
	HapticEffect(42,0,0,0)
#endplatform
	endif
endfunction


function PlayerObject_HandleSpindashCD
	if Player.Direction==FACING_RIGHT
		Screen.CameraStyle=2
	else
		Screen.CameraStyle=3
	endif
	if Player.Gravity==1
		Player.State=PlayerObject_HandleAir
		Player.Speed=0
		Screen.CameraStyle=1
	endif
	if Player.GravityStrength==4096
		if Player.AbilityTimer<524288
			Player.AbilityTimer+=24576
		endif
	else
		if Player.AbilityTimer<786432
			Player.AbilityTimer+=24576
		endif
	endif
	if Player.Down==0
		Screen.CameraStyle=1
		Player.Timer=0
		if Player.AbilityTimer<195297
			Player.Speed=0
			Player.State=PlayerObject_HandleGround
		else
			Player.State=PlayerObject_HandleRolling
			Player.Animation=ANI_JUMPING
			Player.Speed=Player.AbilityTimer
			if Player.Direction==FACING_LEFT
				FlipSign(Player.Speed)
			endif
			PlaySfx(7,0)
		endif
		CallFunction(PlayerObject_ResetOnFloor)
#platform: Use_Haptics
	HapticEffect(42,0,0,0)
#endplatform
	endif
endfunction


function PlayerObject_HandlePeeloutS2
	if Player.Gravity==1
		Player.State=PlayerObject_HandleAir
		Player.Speed=0
	endif
	if Player.GravityStrength==4096
		if Player.AbilityTimer<393216
			Player.AbilityTimer+=24576
		endif
	else
		if Player.AbilityTimer<786432
			Player.AbilityTimer+=24576
		endif
	endif
	if Player.AbilityTimer<390594
		if Stage.PlayerListPos==0
			if Options.WalkAnim==0
				Player.Animation=ANI_WALKING
			else
				Player.Animation=ANI_ALTWALK
			endif
		else
				Player.Animation=ANI_WALKING
		endif
		TempValue0=Player.AbilityTimer
		TempValue0>>=16
		TempValue0*=80
		TempValue0/=6
		TempValue0+=20
	else
		TempValue0=Player.AbilityTimer
		TempValue0>>=16
		TempValue0*=80
		TempValue0/=6
		if Player.AbilityTimer>655359
			Player.Animation=ANI_PEELOUT
		else
			if Stage.PlayerListPos==0
				if Options.RunAnim==0
					Player.Animation=ANI_RUNNING
				else
					Player.Animation=ANI_ALTRUN
				endif
			else
					Player.Animation=ANI_RUNNING
			endif
		endif
	endif
	if Player.Up==0
		Player.ScrollDelay=15
		Screen.CameraStyle=4
		Player.State=PlayerObject_HandleGround
		if Player.AbilityTimer<390594
			Player.Speed=0
		else
			Player.Speed=Player.AbilityTimer
			if Player.Direction==FACING_LEFT
				FlipSign(Player.Speed)
			endif
			PlaySfx(7,0)
		endif
		CallFunction(PlayerObject_ResetOnFloor)
#platform: Use_Haptics
	HapticEffect(42,0,0,0)
#endplatform
	endif
	Player.AnimationSpeed=TempValue0
endfunction


function PlayerObject_HandlePeeloutCD
	if Player.Direction==FACING_RIGHT
		Screen.CameraStyle=2
	else
		Screen.CameraStyle=3
	endif
	if Player.Gravity==1
		Player.State=PlayerObject_HandleAir
		Player.Speed=0
		Screen.CameraStyle=1
	endif
	if Player.GravityStrength==4096
		if Player.AbilityTimer<393216
			Player.AbilityTimer+=24576
		endif
	else
		if Player.AbilityTimer<786432
			Player.AbilityTimer+=24576
		endif
	endif
	if Player.AbilityTimer<390594
		if Stage.PlayerListPos==0
			if Options.WalkAnim==0
				Player.Animation=ANI_WALKING
			else
				Player.Animation=ANI_ALTWALK
			endif
		else
				Player.Animation=ANI_WALKING
		endif
		TempValue0=Player.AbilityTimer
		TempValue0>>=16
		TempValue0*=80
		TempValue0/=6
		TempValue0+=20
	else
		TempValue0=Player.AbilityTimer
		TempValue0>>=16
		TempValue0*=80
		TempValue0/=6
		if Player.AbilityTimer>655359
			Player.Animation=ANI_PEELOUT
		else
			if Stage.PlayerListPos==0
				if Options.RunAnim==0
					Player.Animation=ANI_RUNNING
				else
					Player.Animation=ANI_ALTRUN
				endif
			else
					Player.Animation=ANI_RUNNING
			endif
		endif
	endif
	if Player.Up==0
		Screen.CameraStyle=1
		Player.State=PlayerObject_HandleGround
		if Player.AbilityTimer<390594
			Player.Speed=0
		else
			Player.Speed=Player.AbilityTimer
			if Player.Direction==FACING_LEFT
				FlipSign(Player.Speed)
			endif
			PlaySfx(7,0)
		endif
		CallFunction(PlayerObject_ResetOnFloor)
#platform: Use_Haptics
	HapticEffect(42,0,0,0)
#endplatform
	endif
	Player.AnimationSpeed=TempValue0
endfunction


function PlayerObject_HandleTailsFlight
	CallFunction(PlayerObject_HandleAirSpeed)
	if Player.Gravity==1
		Player.XVelocity=Player.Speed
		if Warp.Destination>0
			TempValue0=Player.XVelocity
			if TempValue0<0
				FlipSign(TempValue0)
			endif
			TempValue1=Player.YVelocity
			if TempValue1<0
				FlipSign(TempValue1)
			endif
			TempValue0+=TempValue1
			if TempValue0<262144
				if Warp.Timer>99
					if Warp.Timer<Warp.MaxTimer
						Warp.Destination=0
					endif
				endif
				Warp.Timer=0
			endif
		endif
		if Player.YVelocity<-65536
			Player.FlightGravity=2048
		else
			if Player.YVelocity<1
				if Player.AbilityTimer<60
					Player.AbilityTimer++
				else
					Player.FlightGravity=2048
				endif
			endif
		endif
		Player.YVelocity+=Player.FlightGravity
		if Player.Timer<480
			if Player.GravityStrength==14336
				Player.Animation=ANI_FLYING
			else
				Player.Animation=ANI_SWIMMING
			endif
			Player.Timer++
			if Player.Timer==480
				if Player.GravityStrength==14336
					Player.Animation=ANI_FLYINGTIRED
					StopSfx(24)
					PlaySfx(25,1)
				else
					Player.Animation=ANI_SWIMMINGTIRED
				endif
			else
				if Player.JumpPress==1
					Player.FlightGravity=-8192
					Player.AbilityTimer=0
				endif
			endif
		else
			if Player.GravityStrength==14336
				Player.Animation=ANI_FLYINGTIRED
			else
				Player.Animation=ANI_SWIMMINGTIRED
			endif
		endif
	else
		Player.State=PlayerObject_HandleGround
		CallFunction(PlayerObject_ResetOnFloor)
	endif
	if Player.RoofBarrier==1
		TempValue0=Player.YPos
		TempValue0>>=16
		if TempValue0<Player.CollisionBottom
			Player.YPos=Player.CollisionBottom
			Player.YPos<<=16
		endif
	endif
endfunction



function PlayerObject_KnucklesGlideDrop
	if Player.Gravity==1
		CallFunction(PlayerObject_HandleMovement)
		CallFunction(PlayerObject_HandleAirMovement)
	else
		if Player.Timer==0
			PlaySfx(SfxName[Landing],0)
		endif
		Player.TrackScroll=0
		Player.Speed=0
		Player.XVelocity=0
		Player.Animation=ANI_LOOKINGDOWN
		Player.PrevAnimation=ANI_LOOKINGDOWN
		Player.Frame=2
		if Options.GenBehaviour == 1
			if Player.Down == 1
				if Player.JumpPress==1
					if Options.OriginalControls==0
						Player.State = PlayerObject_StartSpindashS2
					else
						Player.State = PlayerObject_StartSpindashCD
					endif
				endif
			end if
		end if
		if Player.Timer<16
			Player.Timer++
		else
			Player.State=PlayerObject_HandleGround
			CallFunction(PlayerObject_ResetOnFloor)
			Player.Skidding=0
		endif
	endif
endfunction

function PlayerObject_KnucklesGlideStop
	if Player.Gravity==0
		if Player.Speed==0
			Player.TrackScroll=0
			Player.Frame=1
			if Player.Timer<16
				Player.Timer++
			else
				Player.State=PlayerObject_HandleGround
				CallFunction(PlayerObject_ResetOnFloor)
				Player.Skidding=0
			endif
		else
			if Ring.AniCount==0
				CreateTempObject(TypeName[DustPuff],0,Player.XPos,Player.YPos)
				Object[TempObjectPos].iYPos+=Player.CollisionBottom
				Object[TempObjectPos].DrawOrder=Player.DrawOrder
				if Player.Timer==0
					PlaySfx(SfxName[Sliding],0)
					Player.Timer=1
				else
					Player.Timer=0
				endif
			endif
			Player.Frame=0
			if Player.Speed>0
				Player.Speed-=8192
				if Player.Speed<0
					Player.Speed=0
					Player.Timer=0
				endif
			else
				Player.Speed+=8192
				if Player.Speed>0
					Player.Speed=0
					Player.Timer=0
				endif
			endif
			if Player.JumpHold==0
				Player.Speed=0
				Player.Timer=0
			endif
		endif
		if Options.GenBehaviour == 1
			if Player.Down == 1
				if Player.JumpPress==1
					if Options.OriginalControls==0
						Player.State = PlayerObject_StartSpindashS2
					else
						Player.State = PlayerObject_StartSpindashCD
					endif
				endif
			end if
		end if
		Player.XVelocity=Player.Speed
	else
		Player.Timer=0
		Player.Animation=ANI_GLIDING_DROP
		Player.State=PlayerObject_KnucklesGlideDrop
	endif
endfunction

function PlayerObject_HandleKnuxClimbUp
	if Player.Timer<16
		Player.Timer++
	endif
	switch Player.Timer
	default
		Player.TileCollisions=0
		break
	case 1
		Player.Frame=0
		Player.TileCollisions=1
		if Player.direction == FACING_RIGHT
			Player.XPos+=65536
		else
			Player.XPos-=65536			
		endif
		break
	case 5
		Player.Frame=1
		Player.TileCollisions=0
		if Player.direction == FACING_RIGHT
			Player.XPos+=589824
		else
			Player.XPos-=589824			
		endif
			Player.YPos-=655360
		break
	case 10
		Player.Frame=2
		Player.TileCollisions=0
		if Player.direction == FACING_RIGHT
			Player.XPos+=327680
		else
			Player.XPos-=327680			
		endif
		Player.YPos-=131072
		break
	case 15
		Player.Timer=0
		Player.Animation=ANI_STOPPED
		Player.State=PlayerObject_HandleAir
		if Mini_PlayerFlag==1
			Player.YPos+=131072
		else
			Player.YPos-=655360
		endif
		Player.TileCollisions=1
		break
	endswitch
endfunction

function PlayerObject_Hurt
	Player.TileCollisions=1
	ArrayPos0=Player.EntityNo
	ArrayPos0+=2
	if Object[ArrayPos0].PropertyValue>0
		TempValue0=1
		Player.Shield=0
		ResetObjectEntity(ArrayPos0,TypeName[BlankObject],0,0,0)
		PlaySfx(5,0)
	else
		if Object[0].Value0==0
			PlaySfx(5,0)
			TempValue0=3
		else
			PlaySfx(4,0)
			TempValue0=2
		endif
	endif
	switch TempValue0
	case 1
#platform: Use_Haptics
	HapticEffect(16,0,0,0)
#endplatform
		Player.State=PlayerObject_Knockback
		Player.Animation=ANI_HURT
		Player.YVelocity=-262144
		Player.Gravity=1
		Player.TrackScroll=1
		Player.Invincible=8000
		if Player.GravityStrength==4096
			Player.Speed>>=1
			Player.YVelocity>>=1
		endif
		break
	case 2
#platform: Use_Haptics
	HapticEffect(16,0,0,0)
#endplatform
		if Player.CollisionPlane==0
			TempValue4=3
		else
			TempValue4=1
		endif
		Player.State=PlayerObject_Knockback
		Player.Animation=ANI_HURT
		Player.YVelocity=-262144
		Player.Gravity=1
		Player.TrackScroll=1
		Player.Invincible=8000
		if Player.GravityStrength==4096
			Player.Speed>>=1
			Player.YVelocity>>=1
		endif
		TempValue0=Object[0].Value0
		if TempValue0>16
			TempValue1=TempValue0
			TempValue1-=16
			TempValue0=16
		else
			TempValue1=0
		endif
		if TempValue1>16
			TempValue1=16
		endif
		TempValue3=TempValue1
		TempValue3>>=1
		TempValue3<<=5
		TempValue2=384
		TempValue2-=TempValue3
		TempValue3>>=4
		if TempValue3==TempValue1
			TempValue2+=16
		else
			TempValue2-=16
		endif
		TempValue3=0
		while TempValue3<TempValue1
			CreateTempObject(TypeName[LoseRing],Player.CollisionPlane,Player.XPos,Player.YPos)
			Cos(Object[TempObjectPos].Value0,TempValue2)
			Sin(Object[TempObjectPos].Value1,TempValue2)
			Object[TempObjectPos].Value0<<=8
			Object[TempObjectPos].Value1<<=8
			Object[TempObjectPos].DrawOrder=TempValue4
			Object[TempObjectPos].AnimationSpeed=256
			TempValue3++
			TempValue2+=32
		loop
		TempValue3=TempValue0
		TempValue3>>=1
		TempValue3<<=5
		TempValue2=384
		TempValue2-=TempValue3
		TempValue3>>=4
		if TempValue3==TempValue0
			TempValue2+=16
		else
			TempValue2-=16
		endif
		TempValue3=0
		while TempValue3<TempValue0
			CreateTempObject(TypeName[LoseRing],Player.CollisionPlane,Player.XPos,Player.YPos)
			Cos(Object[TempObjectPos].Value0,TempValue2)
			Sin(Object[TempObjectPos].Value1,TempValue2)
			Object[TempObjectPos].Value0<<=9
			Object[TempObjectPos].Value1<<=9
			Object[TempObjectPos].DrawOrder=TempValue4
			Object[TempObjectPos].AnimationSpeed=256
			TempValue3++
			TempValue2+=32
		loop
		Object[0].Value0=0
		Ring.ExtraLife=100
		break
	case 3
#platform: Use_Haptics
	HapticEffect(28,0,0,0)
#endplatform
		Object.DrawOrder=5
		Player.Speed=0
		Player.YVelocity=-458752
		Player.XVelocity=0
		Player.State=PlayerObject_Death
		Player.Animation=ANI_DYING
		Player.TileCollisions=0
		Player.ObjectInteraction=0
		if Player.EntityNo==0
			Screen.CameraEnabled=0
		endif
		break

	endswitch
	if Warp.Destination>0
		if Warp.Timer>99
			if Warp.Timer<Warp.MaxTimer
				Warp.Destination=0
			endif
		endif
		Warp.Timer=0
	endif
endfunction


function PlayerObject_Knockback
	if Player.Gravity==1
		Player.TrackScroll=1
		if Player.GravityStrength==14336
			Player.YVelocity+=12288
		else
			Player.YVelocity+=3840
		endif
		Player.XVelocity=Player.Speed
	else
		Player.State=PlayerObject_HandleGround
		Player.Invincible=120
		Player.BlinkTimer=3
		Player.Speed=0
		Player.XVelocity=0
		CallFunction(PlayerObject_ResetOnFloor)
	endif
endfunction


function PlayerObject_OuttaHere
	if Player.Timer<140
		Player.Timer++
	else
		Player.Timer=0
		Player.DrawOrder=5
		if Player.Direction==FACING_RIGHT
			Player.Speed=65536
			Player.XVelocity=65536
		else
			Player.Speed=-65536
			Player.XVelocity=-65536
		endif
		Player.YVelocity=-360448
		Player.State=PlayerObject_Death
		Player.TileCollisions=0
		Player.ObjectInteraction=0
		Screen.CameraEnabled=0
	endif
endfunction


function PlayerObject_Death
	Player.ControlMode=-1
	Player.YVelocity+=14336
	if Player.Animation!=ANI_BORED
		Player.Animation=ANI_DYING
	endif
	if Player.YVelocity>1048576
		if Player.Lives>0
			if Player.Animation==ANI_BORED
				Player.Lives=0
			else
				Player.Lives--
			endif
		endif
		Stage.TimeEnabled=0
		Object.Type=TypeName[DeathEvent]
		Object.DrawOrder=6
		Object.Value1=Screen.CenterX
		Object.Value1-=264
		Object.Value2=Screen.CenterX
		Object.Value2+=200
		if Options.GameMode==2
			Object.Value3=0
			Object.State=3
		else
			if Player.Lives==0
				Object.Value3=-2880
				Object.State=0
				PlayMusic(5)
				Stage.PauseEnabled=0
			else
				Object.Value3=0
				Object.State=2
				if Stage.Minutes==9
					if Stage.Seconds==59
						Object.Value3=-2880
						Object.State=1
						PlayMusic(5)
						Stage.PauseEnabled=0
					endif
				endif
			endif
		endif
	endif
endfunction


function PlayerObject_Drown
	Player.ControlMode=-1
	Player.YVelocity+=Player.GravityStrength
	Player.Animation=ANI_DROWNING
	if Player.YVelocity>524288
		if Player.Lives>0
			Player.Lives--
		endif
		Stage.TimeEnabled=0
		Object.Type=TypeName[DeathEvent]
		Object.DrawOrder=6
		Object.Value1=Screen.CenterX
		Object.Value1-=264
		Object.Value2=Screen.CenterX
		Object.Value2+=200
		if Options.GameMode==2
			Object.Value3=0
			Object.State=3
		else
			if Player.Lives==0
				Object.Value3=-2880
				Object.State=0
				PlayMusic(5)
				Stage.PauseEnabled=0
			else
				Object.Value3=0
				Object.State=2
			endif
		endif
	endif
endfunction

//Could be named better
function PlayerObject_CheckTile
	if Player.Left==1
		Player.Direction=FACING_LEFT
		Player.Speed=-131072
		Player.AnimationSpeed=30
	else
		if Player.Right==1
			Player.Direction=FACING_RIGHT
			Player.Speed=131072
			Player.AnimationSpeed=30
		else
			Player.Speed=0
			Player.AnimationSpeed=0
		endif
	endif
	TempValue1=Player.XPos
	TempValue1>>=16
	TempValue2=Player.YPos
	TempValue2>>=16
	TempValue2+=Player.CollisionTop
	Get16x16TileInfo(TempValue0,TempValue1,TempValue2,8)
	if TempValue0!=3
		Player.State=PlayerObject_HandleAir
		Player.Speed=0
		Player.AnimationSpeed=0
		Player.YVelocity=0
	endif
	if Player.JumpPress==1
		Player.State=PlayerObject_HandleAir
		Player.YVelocity=0
		Player.Speed=0
		Player.AnimationSpeed=0
		Player.YPos+=262144
	endif
	Player.XVelocity=Player.Speed
	if Warp.Destination>0
		if Warp.Timer>99
			if Warp.Timer<Warp.MaxTimer
				Warp.Destination=0
			endif
		endif
		Warp.Timer=0
	endif
endfunction

//Could be named better
function PlayerObject_CorkscrewRun
	Player.Angle=0
	CallFunction(PlayerObject_HandleMovement)
	Player.Animation=ANI_CORKSCREW_H
	if Player.Speed<393216
		if Player.Speed>-393216
			if Stage.PlayerListPos==0
				if Options.WalkAnim==0
					Player.Animation=ANI_WALKING
				else
					Player.Animation=ANI_ALTWALK
				endif
			else
					Player.Animation=ANI_WALKING
			endif
			Player.State=PlayerObject_HandleAir
			Player.Rotation=0
			if Player.Speed<0
				Player.Direction=FACING_LEFT
			endif
		endif
	endif
	if Player.Down==1
		if Player.Speed>6554
			Player.State=PlayerObject_CorkscrewRoll
			Player.Animation=ANI_JUMPING
		endif
		if Player.Speed<-6554
			Player.State=PlayerObject_CorkscrewRoll
			Player.Animation=ANI_JUMPING
		endif
	endif
	if Player.Skidding>0
		if Player.Skidding==16
			PlaySfx(8,0)
		endif
		Player.Animation=ANI_SKIDDING
		Player.Skidding--
	endif
	if Player.JumpPress==1
		CallFunction(PlayerObject_StartJump)
	else
		CallFunction(PlayerObject_ResetOnFloor)
	endif
endfunction

//Could be named better
function PlayerObject_CorkscrewRoll
	Player.Angle=0
	CallFunction(PlayerObject_HandleRollDecel)
	if Player.Speed<393216
		if Player.Speed>-393216
			Player.State=PlayerObject_HandleAir
		endif
	endif
	if Player.JumpPress==1
		CallFunction(PlayerObject_StartJump)
	else
		CallFunction(PlayerObject_ResetOnFloor)
	endif
endfunction

function PlayerObject_Tunnel
	if Player.Gravity==1
		Player.State=PlayerObject_HandleAir
		Player.Timer=0
		CallFunction(PlayerObject_HandleAirMovement)
	else
		if Player.Speed>0
			if Player.Speed<Player.MinRollSpeed
				Player.Speed=Player.MinRollSpeed
			endif
			if Player.Speed>1048576
				Player.Speed=1048576
			endif
		else
			TempValue0=Player.MinRollSpeed
			FlipSign(TempValue0)
			if Player.Speed>TempValue0
				Player.Speed=TempValue0
				CallFunction(PlayerObject_ResetOnFloor)
			endif
			if Player.Speed<-1048576
				Player.Speed=-1048576
			endif
		endif
		TempValue0=Player.Speed
		if TempValue0<0
			FlipSign(TempValue0)
		endif
		if TempValue0<390594
			if Warp.Destination>0
				if Warp.Timer>99
					if Warp.Timer<Warp.MaxTimer
						Warp.Destination=0
					endif
				endif
				Warp.Timer=0
			endif
		else
			if Warp.Destination>0
				if Warp.Timer==0
					Warp.Timer=1
					CreateTempObject(TypeName[WarpStar],0,Player.XPos,Player.YPos)
					Object[TempObjectPos].Value0=7
					Object[TempObjectPos].DrawOrder=4
				endif
			endif
		endif
		CallFunction(PlayerObject_HandleRollDecel)
		CallFunction(PlayerObject_RollAnimSpeed)
		Player.AnimationSpeed=Player.CustomAnimSpeed
		CallFunction(PlayerObject_ResetOnFloor)
	endif
endfunction


function PlayerObject_StartTTCtrlLock
	Player.TileCollisions=1
	Player.Right=0
	Player.Left=0
	if Player.Gravity==1
		Player.State=PlayerObject_HandleAir
		Player.Timer=0
		CallFunction(PlayerObject_HandleAirMovement)
	else
		if Player.Speed>0
			Player.Speed=Player.MinRollSpeed
		else
			TempValue0=Player.MinRollSpeed
			FlipSign(TempValue0)
			Player.Speed=TempValue0
		endif
		TempValue0=Player.Speed
		if TempValue0<0
			FlipSign(TempValue0)
		endif
		if TempValue0<390594
			if Warp.Destination>0
				if Warp.Timer>99
					if Warp.Timer<Warp.MaxTimer
						Warp.Destination=0
					endif
				endif
				Warp.Timer=0
			endif
		else
			if Warp.Destination>0
				if Warp.Timer==0
					Warp.Timer=1
					CreateTempObject(TypeName[WarpStar],0,Player.XPos,Player.YPos)
					Object[TempObjectPos].Value0=7
					Object[TempObjectPos].DrawOrder=4
				endif
			endif
		endif
		CallFunction(PlayerObject_HandleRollDecel)
		CallFunction(PlayerObject_RollAnimSpeed)
		Player.AnimationSpeed=Player.CustomAnimSpeed
		CallFunction(PlayerObject_ResetOnFloor)
	endif
endfunction


function PlayerObject_SpinningTop
	Player.Timer+=Player.MinRollSpeed
	if Player.Gravity==1
		Player.State=PlayerObject_HandleAir
		if Stage.PlayerListPos==0
			if Options.WalkAnim==0
				Player.Animation=ANI_WALKING
			else
				Player.Animation=ANI_ALTWALK
			endif
		else
				Player.Animation=ANI_WALKING
		endif
		Player.Timer=0
		CallFunction(PlayerObject_HandleAirMovement)
	else
		CallFunction(PlayerObject_ResetOnFloor)
	endif
endfunction


function PlayerObject_Hugged
	if Player.Gravity==1
		Player.State=PlayerObject_HandleAir
		if Stage.PlayerListPos==0
			if Options.WalkAnim==0
				Player.Animation=ANI_WALKING
			else
				Player.Animation=ANI_ALTWALK
			endif
		else
				Player.Animation=ANI_WALKING
		endif
		Player.Timer=0
		CallFunction(PlayerObject_HandleAirMovement)
	else
		CallFunction(PlayerObject_ResetOnFloor)
		if Player.Timer<240
			Player.Animation=ANI_STOPPED
			Player.Timer++
		else
			Player.Animation=ANI_WAITING
		endif
		if Warp.Destination>0
			if Warp.Timer>99
				if Warp.Timer<Warp.MaxTimer
					Warp.Destination=0
				endif
			endif
			Warp.Timer=0
		endif
		if Player.JumpPress==1
			CallFunction(PlayerObject_StartJump)
		endif
	endif
endfunction


function PlayerObject_Handle3DRampAnims
	CallFunction(PlayerObject_HandleMovement)
	if Player.Gravity==1
		Player.State=PlayerObject_HandleAir
		Player.Timer=0
		CallFunction(PlayerObject_HandleAirMovement)
		if Player.Direction==FACING_LEFT
			if Player.Animation==ANI_RAMP_RUNNING3
				Player.Animation=ANI_RAMP_RUNNING5
			endif
			if Player.Animation==ANI_ALTRUN
				Player.Animation=ANI_RAMP_RUNNING6
			endif
		endif
	else
		CallFunction(PlayerObject_ResetOnFloor)
		if Player.Speed==0
			if Warp.Destination>0
				if Warp.Timer>99
					if Warp.Timer<Warp.MaxTimer
						Warp.Destination=0
					endif
				endif
				Warp.Timer=0
			endif
			if Player.Timer<240
				Player.Animation=ANI_STOPPED
				Player.Timer++
			else
				Player.Animation=ANI_WAITING
			endif
		else
			if Player.Angle==0
				Player.Animation=ANI_ALTWALK
			endif
			if Player.Angle>200
				if Player.Direction==FACING_RIGHT
					Player.Animation=ANI_RAMP_RUNNING3
				else
					Player.Animation=ANI_RAMP_RUNNING5
				endif
			endif
			if Player.Angle>216
				if Player.Direction==FACING_RIGHT
					Player.Animation=ANI_ALTRUN
				else
					Player.Animation=ANI_RAMP_RUNNING6
				endif
			endif
			if Player.Angle>232
				Player.Animation=ANI_ALTWALK
			endif
			if Player.Angle==192
				Player.Animation=ANI_RAMP_RUNNING4
			endif
		endif
		if Player.JumpPress==1
			CallFunction(PlayerObject_StartJump)
		else
			if Player.Up==1
				if Player.Speed==0
					Player.State=PlayerObject_LookingUp
					Player.Animation=ANI_LOOKINGUP
					Player.Timer=0
				endif
			endif
			if Player.Down==1
				if Player.Speed==0
					Player.State=PlayerObject_Crouching
					Player.Animation=ANI_LOOKINGDOWN
					Player.Timer=0
				else
					if Player.Speed>6554
						Player.ControlLock=0
						Player.State=PlayerObject_HandleRolling
						Player.Animation=ANI_JUMPING
					endif
					if Player.Speed<-6554
						Player.ControlLock=0
						Player.State=PlayerObject_HandleRolling
						Player.Animation=ANI_JUMPING
					endif
				endif
			endif
		endif
	endif
	if Player.Animation!=ANI_STOPPED
		Player.AnimationSpeed=Player.Speed
		if Player.AnimationSpeed<0
			FlipSign(Player.AnimationSpeed)
		endif
		Player.AnimationSpeed*=60
		Player.AnimationSpeed/=393216
		Player.AnimationSpeed+=20
	else
		Player.AnimationSpeed=0
	endif
endfunction


function PlayerObject_DebugMode
	Player.Gravity=1
	if Player.Up==1
		Player.YPos-=131072
	endif
	if Player.Down==1
		Player.YPos+=131072
	endif
	if Player.Left==1
		Player.XPos-=131072
	endif
	if Player.Right==1
		Player.XPos+=131072
	endif
endfunction


function PlayerObject_SizeChange
	if Player.Gravity==1
		Player.TrackScroll=1
		Player.YVelocity+=12288
		Player.XVelocity=Player.Speed
	else
		Player.State=PlayerObject_HandleGround
		Player.Invincible=120
		Player.BlinkTimer=3
		Player.Speed=0
		Player.XVelocity=0
		CallFunction(PlayerObject_ResetOnFloor)
		if Mini_PlayerFlag==0
			Mini_PlayerFlag=1
			Player.CameraOffset=-1
			switch Stage.PlayerListPos
			case 0
				LoadAnimation("MiniSonic.Ani")
				break
			case 1
				LoadAnimation("MiniTails.Ani")
				Object[1].Type=TypeName[BlankObject]
				break
			case 2
				LoadAnimation("MiniKnuckles.Ani")
				break
			endswitch
		else
			Mini_PlayerFlag=0
			switch Stage.PlayerListPos
			case 0
				LoadAnimation("Sonic.Ani")
				Player.CameraOffset=-5
				break
			case 1
				LoadAnimation("Tails.Ani")
				Player.CameraOffset=-1
				Object[1].Type=TypeName[TailsObject]
				break
			case 2
				LoadAnimation("Knuckles.Ani")
				Player.CameraOffset=-5
				break

			endswitch
		endif
		BindPlayerToObject(0,0)
	endif
endfunction

function PlayerObject_VictoryAni
	Player.ControlMode=-1
	Player.Animation=ANI_FINISH_POSE
	Player.XVelocity=0
	Player.Up=false
	Player.Down=false
	Player.Left=false
	Player.JumpPress=false
	Player.JumpHold=false
endfunction

function PlayerObject_CheckShield
	if Player.JumpPress==1
		switch Player.Shield
		case 0 //No shield/Insta-Shield
			break
		case 1 //Blue Shield
			break
		case 2 //Bubble Shield
			PlaySfx(33,0)
			player.yvelocity=0x80000
			player.xvelocity=0
			player.speed=player.xvelocity
			player.state=PlayerObject_BubbleBounce
			object[2].state = 2
			break
		case 3 //Fire Shield
			PlaySfx(35,0)
			if Player.Direction==FACING_RIGHT
				player.xvelocity=0x80000
			else
				player.xvelocity=-0x80000
			end if
			player.speed=player.xvelocity
			player.yvelocity=0
			Player.State=PlayerObject_AbilityLock
			object[2].state = 2
			object[2].direction = player.direction
			Player.ScrollDelay=15
			Screen.CameraStyle=4
			break
		case 4 //Lightning Shield
			PlaySfx(37,0)
			player.yvelocity=-0x58000
			Player.State=PlayerObject_AbilityLock
			CreateTempObject(TypeName[Lightning Spark], 0, player.xpos, player.ypos)
			object[tempObjectPos].value0 = -0x20000
			object[tempObjectPos].value1 = -0x20000
			CreateTempObject(TypeName[Lightning Spark], 0, player.xpos, player.ypos)
			object[tempObjectPos].value0 = 0x20000
			object[tempObjectPos].value1 = -0x20000
			CreateTempObject(TypeName[Lightning Spark], 0, player.xpos, player.ypos)
			object[tempObjectPos].value0 = -0x20000
			object[tempObjectPos].value1 = 0x20000
			CreateTempObject(TypeName[Lightning Spark], 0, player.xpos, player.ypos)
			object[tempObjectPos].value0 = 0x20000
			object[tempObjectPos].value1 = 0x20000
			break
		endswitch
	endif
endfunction

function PlayerObject_AbilityLock //This is PlayerObject_AirControlLock lol
	CallFunction(PlayerObject_HandleAirSpeed)
	if Player.Gravity==1
		CallFunction(PlayerObject_HandleAirMovement)
	else
		Player.State=PlayerObject_HandleGround
		CallFunction(PlayerObject_ResetOnFloor)
	endif
endfunction

function PlayerObject_BubbleBounce
	CallFunction(PlayerObject_HandleAirSpeed)
	CheckNotEqual(player.shield, 2)
	TempValue0=CheckResult
	CheckNotEqual(Player.Invincible, 0)
	TempValue0|=CheckResult
	if TempValue0==True
		Player.State=PlayerObject_HandleAir
	else
		if player.gravity == 1
			CallFunction(PlayerObject_HandleAirMovement)
		else
			player.gravity = 1
			if player.jumpStrength == 0x38000
				TempValue1 = -0x40000
			else
				TempValue1 = -0x78000
			end if
			TempValue1 += player.gravityStrength
			Sin256(player.xvelocity, player.angle)
			player.xvelocity *= TempValue1
			Cos256(TempValue0, player.angle)
			TempValue0 *= player.speed
			player.xvelocity += TempValue0
			player.xvelocity >>= 8

			Sin256(player.yvelocity, player.angle)
			player.yvelocity *= player.speed
			Cos256(TempValue0, player.angle)
			TempValue0 *= TempValue1
			player.yvelocity = TempValue0
			player.yvelocity >>= 8
			Player.TrackScroll= True
			player.speed = player.xvelocity
			player.animation = ANI_JUMPING
			player.angle = 240
			player.collisionMode = 0
			player.timer = 1
			CallFunction(PlayerObject_RollAnimSpeed)
			player.state = PlayerObject_HandleAir
			PlaySfx(33,0)
			object[2].state = 4
		end if
	end if
endfunction
	

sub ObjectMain
#platform: Standard
	if Stage.DebugMode==1
		if KeyPress.ButtonB==1
			Object.Type=TypeName[DebugMode]
			Player.YVelocity=0
			Player.State=PlayerObject_Blank
			Player.Frame=0
			Player.Rotation=0
			Player.ObjectInteraction=false
			Player.TileCollisions=true
			Player.DrawOrder=4
			Player.AbilityTimer=0
			Player.MinRollSpeed=0
			Screen.CameraEnabled=true
			Screen.CameraStyle=Options.OriginalControls
		else
			CallFunction(PlayerObject_ProcessPlayer)
			CallFunction(Player.State)
			ProcessAnimation()
			if Player.Animation==ANI_JUMPING
				Screen.AdjustCameraY=Player.CameraOffset
			else
				if Screen.AdjustCameraY==Player.CameraOffset
					Screen.AdjustCameraY=0
					Player.iYPos+=Player.CameraOffset
				endif
			endif
			if Player.YVelocity>1048576
				Player.YVelocity=1048576
			endif
			//dont let this set our rotation
			TempValue1 = Player.Rotation
			PlayerTileCollision()
			Player.Rotation = TempValue1
			if CheckResult==true
				if Player.Animation==ANI_JUMPING
					if Player.Down==false
						if Stage.PlayerListPos==0
							if Options.WalkAnim==0
								Player.Animation=ANI_WALKING
							else
								Player.Animation=ANI_ALTWALK
							endif
						else
								Player.Animation=ANI_WALKING
						endif
						Screen.AdjustCameraY=0
						Player.iYPos+=Player.CameraOffset
					endif
					Player.Rotation = Player.Angle
					Player.Rotation <<=1
				endif
			endif
		endif
	else
		CallFunction(PlayerObject_ProcessPlayer)
		CallFunction(Player.State)
		ProcessAnimation()
		if Player.Animation==ANI_JUMPING
			Screen.AdjustCameraY=Player.CameraOffset
		else
			if Screen.AdjustCameraY==Player.CameraOffset
				Screen.AdjustCameraY=0
				Player.iYPos+=Player.CameraOffset
			endif
		endif
		if Player.YVelocity>1048576
			Player.YVelocity=1048576
		endif
		//dont let this set our rotation
		TempValue1 = Player.Rotation
		PlayerTileCollision()
		Player.Rotation = TempValue1
		if CheckResult==true
			if Player.Animation==ANI_JUMPING
				if Player.Down==false
					if Stage.PlayerListPos==0
						if Options.WalkAnim==0
							Player.Animation=ANI_WALKING
						else
							Player.Animation=ANI_ALTWALK
						endif
					else
							Player.Animation=ANI_WALKING
					endif
					Screen.AdjustCameraY=0
					Player.iYPos+=Player.CameraOffset
				endif
				Player.Rotation = Player.Angle
				Player.Rotation <<=1
			endif
		endif
	endif
#endplatform

#platform: Mobile
	CallFunction(PlayerObject_ProcessPlayer)
	CallFunction(Player.State)
	ProcessAnimation()
	if Player.Animation==ANI_JUMPING
		Screen.AdjustCameraY=Player.CameraOffset
	else
		if Screen.AdjustCameraY==Player.CameraOffset
			Screen.AdjustCameraY=0
			Player.iYPos+=Player.CameraOffset
		endif
	endif
	if Player.YVelocity>1048576
		Player.YVelocity=1048576
	endif
	//dont let this set our rotation
	TempValue1 = Player.Rotation
	PlayerTileCollision()
	Player.Rotation = TempValue1
	if CheckResult==true
		if Player.Animation==ANI_JUMPING
			if Player.Down==false
				if Stage.PlayerListPos==0
					if Options.WalkAnim==0
						Player.Animation=ANI_WALKING
					else
						Player.Animation=ANI_ALTWALK
					endif
				else
						Player.Animation=ANI_WALKING
				endif
				Screen.AdjustCameraY=0
				Player.iYPos+=Player.CameraOffset
			endif
			Player.Rotation = Player.Angle
			Player.Rotation <<=1
		endif
	endif
#endplatform
endsub


sub ObjectDraw
	if Player.Animation!=Player.PrevAnimation
		Player.PrevAnimation=Player.Animation
		Player.Frame=0
		Player.AnimationTimer=0
		Player.AnimationSpeed=0
	endif
	DrawPlayerAnimation()
endsub


sub ObjectStartup
	#platform: Standard
		Warp.MaxTimer=220
	#endplatform
	#platform: Mobile
		Warp.MaxTimer=204
	#endplatform
	ArrayPos0=32
	while ArrayPos0<1056
		if Object[ArrayPos0].Type==TypeName[PlayerObject]
			switch Stage.PlayerListPos
			case 0
				ResetObjectEntity(0,TypeName[PlayerObject],0,Object[ArrayPos0].XPos,Object[ArrayPos0].YPos)
				Player.XPos=Object[ArrayPos0].XPos
				Player.YPos=Object[ArrayPos0].YPos
				LoadAnimation("Sonic.Ani")
				BindPlayerToObject(0,0)
				Player.State=PlayerObject_HandleAir
				Player.Priority=1
				Player.DrawOrder=4
				Player.TopSpeed=393216
				Player.Acceleration=3072
				Player.Deceleration=3072
				Player.AirAcceleration=6144
				Player.AirDeceleration=1536
				Player.GravityStrength=14336
				Player.JumpStrength=425984
				Player.JumpCap=-262144
				Player.RollingDeceleration=8192
				Player.CameraOffset=-5
				Player.FlightFunction=PlayerObject_CheckShield
				if Options.OriginalControls==0
					Player.PeeloutFunction=PlayerObject_StartPeeloutS2
					Player.SpindashFunction=PlayerObject_StartSpindashS2
				else
					Player.PeeloutFunction=PlayerObject_StartPeeloutCD
					Player.SpindashFunction=PlayerObject_StartSpindashCD
				endif
				break
			case 1
				ResetObjectEntity(0,TypeName[PlayerObject],0,Object[ArrayPos0].XPos,Object[ArrayPos0].YPos)
				Player.XPos=Object[ArrayPos0].XPos
				Player.YPos=Object[ArrayPos0].YPos
				LoadAnimation("Tails.Ani")
				BindPlayerToObject(0,0)
				Player.State=PlayerObject_HandleAir
				Player.Priority=1
				Player.DrawOrder=4
				Player.TopSpeed=393216
				Player.Acceleration=3072
				Player.Deceleration=3072
				Player.AirAcceleration=6144
				Player.AirDeceleration=1536
				Player.GravityStrength=14336
				Player.JumpStrength=425984
				Player.JumpCap=-262144
				Player.RollingDeceleration=8192
				Player.CameraOffset=-1
				Player.FlightFunction=PlayerObject_StartTailsFlight
				Player.PeeloutFunction=PlayerObject_StartJump
				if Options.OriginalControls==0
					Player.SpindashFunction=PlayerObject_StartSpindashS2
				else
					Player.SpindashFunction=PlayerObject_StartSpindashCD
				endif
				break
			case 2
				ResetObjectEntity(0,TypeName[PlayerObject],0,Object[ArrayPos0].XPos,Object[ArrayPos0].YPos)
				Player.XPos=Object[ArrayPos0].XPos
				Player.YPos=Object[ArrayPos0].YPos
				LoadAnimation("Knuckles.Ani")
				BindPlayerToObject(0,0)
				Player.State=PlayerObject_HandleAir
				Player.Priority=1
				Player.DrawOrder=4
				Player.TopSpeed=393216
				Player.Acceleration=3072
				Player.Deceleration=3072
				Player.AirAcceleration=6144
				Player.AirDeceleration=1536
				Player.GravityStrength=14336
				Player.JumpStrength=393216
				Player.JumpCap=-262144
				Player.RollingDeceleration=8192
				Player.CameraOffset=-5
				Player.FlightFunction=PlayerObject_StartKnuxGlide
				Player.PeeloutFunction=PlayerObject_StartJump
				if Options.OriginalControls==0
					Player.SpindashFunction=PlayerObject_StartSpindashS2
				else
					Player.SpindashFunction=PlayerObject_StartSpindashCD
				endif
				break
			endswitch
			GetAnimationByName(ANI_HANGING,"Hanging")
			GetAnimationByName(ANI_DROPPING,"Dropping")
			GetAnimationByName(ANI_CLINGING,"Clinging On")
			GetAnimationByName(ANI_LAUNCHER,"Launcher")
			GetAnimationByName(ANI_RAMP_RUNNING3,"3D Ramp 3")
			GetAnimationByName(ANI_RAMP_RUNNING4,"3D Ramp 4")
			GetAnimationByName(ANI_RAMP_RUNNING5,"3D Ramp 5")
			GetAnimationByName(ANI_RAMP_RUNNING6,"3D Ramp 6")
			GetAnimationByName(ANI_ROLL3D,"3D Ramp 7")
			GetAnimationByName(ANI_SIZE_CHANGE,"Size Change")
			GetAnimationByName(ANI_CORKSCREW_H,"Corkscrew H")
			GetAnimationByName(ANI_CORKSCREW_V,"Corkscrew V")
			ResetObjectEntity(ArrayPos0,TypeName[BlankObject],0,0,0)
		endif
		ArrayPos0++
	loop
endsub

sub RSDK
	LoadSpriteSheet("Global/Display.gif")
	SetEditorIcon(Icon0,SingleIcon,-16,-16,32,32,1,143)
endsub
