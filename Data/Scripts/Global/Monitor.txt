//------------Sonic CD Monitor Script-------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

//-------Aliases-------//
#alias 11: TYPE_MONITOR

sub ObjectMain
	if Object.State==1
		Object.Value1+=14336
		Object.YPos+=Object.Value1
		if Object.Value1>=0
			ObjectTileCollision(0,0,16,0)
			if CheckResult==1
				Object.Value1=0
				Object.State=0
			endif
		endif
	endif
endsub


sub ObjectPlayerInteraction
	if Player.CollisionPlane==Object.Value2
#platform: Mobile
		TempValue0=0
		if Player.Gravity==1
			if Player.YVelocity<0
				TempValue0=1
			endif
		endif
		if TempValue0==0		
#endplatform
#platform: Standard	  
		if Player.YVelocity>=0
#endplatform				
			if Player.Animation==ANI_JUMPING
				PlayerObjectCollision(C_TOUCH,-16,-14,16,16)
				if CheckResult==1
					Object.State=0
					CreateTempObject(TypeName[SmokePuff],0,Object.XPos,Object.YPos)
					Object[TempObjectPos].DrawOrder=4
					FlipSign(Player.YVelocity)
					if Player.YVelocity>-131072
						Player.YVelocity=-131072
					endif
					Object.Type=TypeName[BrokenMonitor]
					Object.Priority=1
					Object.Alpha=255
					Object.Value0=Object.YPos
					Object.Value1=-196608
					PlaySfx(8,0)
#platform: Use_Haptics
					HapticEffect(16,0,0,0)
#endplatform
				endif
			else
				PlayerObjectCollision(C_BOX,-15,-16,15,16)
			endif
		else
			PlayerObjectCollision(C_BOX,-15,-16,15,16)
			if CheckResult==4
				Object.State=1
				Object.Value1=-131072
				Player.YVelocity=131072
			endif
		endif
	endif
endsub


sub ObjectDraw
	Object.Frame++
	if Object.Frame>17
		Object.Frame=0
	endif
	switch Object.Frame
	case 0
	case 1
		DrawSprite(0)
		break
	case 6
	case 7
		DrawSprite(9)
		break
	case 12
	case 13
		DrawSprite(10)
		break
	case 14
		DrawSprite(11)
		break
	default
		DrawSprite(Object.PropertyValue)
		endswitch
endsub



sub ObjectStartup
	LoadSpriteSheet("NexusGlobals/Items.gif")
	
	//Monitor Frames
	SpriteFrame(-16,-16,32,32,51,298) //Blank Monitor
	SpriteFrame(-16,-16,32,32,18,34) //Ring Monitor
	SpriteFrame(-16,-16,32,32,18,67) //Shield Monitor
	SpriteFrame(-16,-16,32,32,18,100) //Invincibility Monitor
	SpriteFrame(-16,-16,32,32,18,133) //Speed Shoes Monitor
	SpriteFrame(-16,-16,32,32,18,166) //Sonic Extra Life Monitor
	SpriteFrame(-16,-16,32,32,51,265) //Stopwatch Monitor
	SpriteFrame(-16,-16,32,32,18,265) //Tails Extra Life Monitor
	SpriteFrame(-16,-16,32,32,18,298) //Super Monitor
	SpriteFrame(-16,-16,32,32,18,1) //Static Monitor 1
	SpriteFrame(-16,-16,32,32,18,232) //Static Monitor 2
	SpriteFrame(-16,-16,32,32,18,199) //Robotnik Monitor
	ArrayPos0=32
	while ArrayPos0<1056
		if Object[ArrayPos0].Type==TypeName[Monitor]
			if Object[ArrayPos0].PropertyValue>9
				Object[ArrayPos0].PropertyValue-=10
				Object[ArrayPos0].DrawOrder=1
				Object[ArrayPos0].Value2=1
			endif
		endif
		ArrayPos0++
	loop
	if Stage.PlayerListPos==1
		ArrayPos0=32
		while ArrayPos0<1056
			if Object[ArrayPos0].Type==TypeName[Monitor]
				if Object[ArrayPos0].PropertyValue==5
					Object[ArrayPos0].PropertyValue=7
				endif
			endif
			ArrayPos0++
		loop
	endif
	if Options.GameMode==2
		ArrayPos0=32
		while ArrayPos0<1056
			if Object[ArrayPos0].Type==TypeName[Monitor]
				if Object[ArrayPos0].PropertyValue==5
					Object[ArrayPos0].Type=TypeName[BlankObject]
				endif
			endif
			ArrayPos0++
		loop
	endif
endsub

sub RSDK
	LoadSpriteSheet("Global/Display.gif")
	SetEditorIcon(Icon0,SingleIcon,-16,-16,32,32,1,143)
endsub
