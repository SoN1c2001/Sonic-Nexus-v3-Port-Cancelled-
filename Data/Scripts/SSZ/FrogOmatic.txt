//--------------Sonic Nexus Frog Badnik---------------//
//----Scripted by Christian Whitehead "The Taxman"----//

sub ObjectMain
	Object.Value1+=12288

	Object.XPos+=Object.Value0
	Object.YPos+=Object.Value1

	if Object.Value1>=0
		ObjectTileCollision(0,0,20,0)
		if CheckResult==1
			Object.Value1=-262144
			Object.Value2++
			if Object.Value2==4
				Object.Value2=0
				FlipSign(Object.Value0)
			end if
		end if
	end if
end sub

sub ObjectPlayerInteraction
	if Object.State<5
		ArrayPos0=Player.EntityNo
		ArrayPos0+=2
		if Object[ArrayPos0].Type==TypeName[Ring Sparkle]
			PlayerObjectCollision(C_TOUCH,-24,-24,24,24)
		else
			PlayerObjectCollision(C_TOUCH,-15,-10,15,10)
		endif
		if CheckResult==1
			CallFunction(PlayerObject_BadnikBreak)
		endif
	endif
endsub

sub ObjectDraw
	if Object.Value0>0
		Object.Direction=1
		Object.Frame=0
		Object.Value3=3
	else
		Object.Direction=0
		Object.Frame=0
		Object.Value3=3
	end if

	if Object.Value1>131072
		Object.Frame+=2
		Object.Value3+=2
	else
		if Object.Value1>32768
			Object.Frame++
			Object.Value3++
		end if
	end if

	if Object.Value1<-196608
		Object.Frame+=2
		Object.Value3+=2
	else
		if Object.Value1<-131072
			Object.Frame++
			Object.Value3++
		end if
	end if

	DrawSpriteFX(Object.Frame,FX_FLIP,Object.XPos,Object.YPos)
	DrawSprite(Object.Value3)
end sub

sub ObjectStartup
	LoadSpriteSheet("SSZ/Objects.gif")
	
	//Frog Left Frames
	SpriteFrame(-8,-16,16,47,77,80)  //Frog Fully Extend
	SpriteFrame(-8,-16,16,38,94,89) //Frog Mid Extend
	SpriteFrame(-8,-16,16,29,111,98) //Frog No Extend
	//Spring
	SpriteFrame(-8,31,16,6,58,1) //Spring Fully Extend
	SpriteFrame(-8,22,16,6,58,1) //Spring Mid Extend
	SpriteFrame(-8,13,16,6,58,1) //Spring No Extend
	ArrayPos0=32
	while ArrayPos0<1056
		if Object[ArrayPos0].Type==TypeName[FrogOmatic] //this object type check was originally missing for some reason
			Object[ArrayPos0].Value0=-65536
		endif
		ArrayPos0++
	loop
end sub

sub RSDK
	LoadSpriteSheet("SSZ/Objects.gif")
	SetEditorIcon(Icon0,SingleIcon,-8,-16,16,35,35,1)
end sub