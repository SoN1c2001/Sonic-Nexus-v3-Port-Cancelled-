//----------Sonic Nexus SSZ Floating Platform---------//
//----Scripted by Christian Whitehead "The Taxman"----//

#alias Object.Value0 : Object.Pressed
#alias Object.Value1 : Object.PlatformY
#alias Object.Value2 : Object.ChangeY

sub ObjectMain
	if Object.Pressed==true
		if Object.PlatformY<16
			Object.PlatformY++
			TempValue0=Object.PlatformY
			TempValue0&=3
			if TempValue0==3
				Object.ChangeY=65536
			else
				Object.ChangeY=0
			end if
		else
			Object.ChangeY=0
		end if
	else
		if Object.PlatformY>0
			Object.PlatformY--
			TempValue0=Object.PlatformY
			TempValue0&=3
			if TempValue0==3
				Object.ChangeY=-65536
			else
				Object.ChangeY=0
			end if
		else
			Object.ChangeY=0
		end if
	end if

	Object.Pressed=false
end sub

sub ObjectPlayerInteraction
	PlayerObjectCollision(C_PLATFORM,-32,-16,32,8)
	if CheckResult==1
		Object.Pressed=true
		Player.YPos+=Object.ChangeY
	end if
end sub

sub ObjectDraw
	Object.YPos+=Object.ChangeY
	DrawSprite(0)
end sub

sub ObjectStartup
	LoadSpriteSheet("SSZ/Objects2.gif")
	
	//Platform Frame
	SpriteFrame(-35,-18,70,36,208,89)
end sub

sub RSDK
	LoadSpriteSheet("SSZ/Objects.gif")
	SetEditorIcon(Icon0,SingleIcon,-35,-18,70,36,160,1)
end sub