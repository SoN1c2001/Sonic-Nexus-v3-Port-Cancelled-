//----------Sonic Nexus Hell-i-bomber Badnik----------//
//----Scripted by Christian Whitehead "The Taxman"----//

#alias Object.Value0 : Object.XVelocity
#alias Object.Value1 : Object.YVelocity
#alias Object.Value2 : Object.DestX
#alias Object.Value3 : Object.Timer
#alias Object.Value4 : Object.Blades
#alias Player.Value4:Player.Invincible

#alias 30 : TYPE_HBOMBER

#alias 9 : ANI_SPINDASH
#alias 10 : ANI_JUMPING
#alias 15 : TYPE_EXPLOSION
#alias 20 : TYPE_INVINCIBLE

#alias 0 :HELLIBOMBER_IDLE
#alias 1 :HELLIBOMBER_MOVE
#alias 2 :HELLIBOMBER_TARGETING
#alias 3 :HELLIBOMBER_SHOOTING

sub ObjectMain

	switch Object.State
	case HELLIBOMBER_IDLE
		Object.Frame=0
		break
	case HELLIBOMBER_MOVE
		Object.Frame=0
		Object.XVelocity=Object.DestX
		Object.XVelocity-=Object.XPos
		Object.XVelocity>>=5

		TempValue0=Object.DestX
		TempValue1=Object.XPos
		TempValue0>>=19
		TempValue1>>=19
		if TempValue0==TempValue1
			Object.State=HELLIBOMBER_TARGETING
			Object.Timer=0
		end if

		if Object.XVelocity>0
			Object.Direction=1
		else
			Object.Direction=0
		end if

		break
	case HELLIBOMBER_TARGETING
		Object.Frame=0
		Object.Timer++
		if Object.Timer==15
			Object.State=HELLIBOMBER_SHOOTING
		end if
		break
	case HELLIBOMBER_SHOOTING
		Object.Frame=1
		Object.Timer++

		if Object.Timer==90
			TempValue0=Object.Type
			TempValue0++
			CreateTempObject(TempValue0,0,Object.XPos,Object.YPos)
			if Object.Direction==1
				Object[TempObjectPos].XPos+=524288
			else
				Object[TempObjectPos].XPos-=524288
			end if

			Object[TempObjectPos].YPos+=1179648

			Object[TempObjectPos].XVelocity=Player.XPos
			Object[TempObjectPos].XVelocity-=Object[TempObjectPos].XPos
			Object[TempObjectPos].XVelocity/=48

			Object[TempObjectPos].YVelocity=Player.YPos
			Object[TempObjectPos].YVelocity-=Object[TempObjectPos].YPos
			Object[TempObjectPos].YVelocity/=48

			if Object[TempObjectPos].YVelocity<0
				FlipSign(Object[TempObjectPos].YVelocity)
			end if
		end if

		if Object.Timer==120
			Object.State=HELLIBOMBER_IDLE
		end if
		break
	end switch


	TempValue0=Oscillation
	TempValue0<<=2
	Sin(Object.YVelocity,TempValue0)

	Object.YVelocity<<=6

	Object.XPos+=Object.XVelocity
	Object.YPos+=Object.YVelocity

end sub

sub ObjectPlayerInteraction

	switch Object.State
	case HELLIBOMBER_IDLE
		PlayerObjectCollision(C_TOUCH,-128,-256,128,256)
		if CheckResult==1
			Object.DestX=Player.XPos
			if Player.XPos>Object.XPos
				Object.DestX+=6291456
			else
				Object.DestX-=6291456
			end if
			Object.State=HELLIBOMBER_MOVE
		end if
		break
	case HELLIBOMBER_TARGETING
	case HELLIBOMBER_SHOOTING
		if Object.XPos<Player.XPos
			Object.Direction=1
		else
			Object.Direction=0
		end if
		break
	end switch

	PlayerObjectCollision(C_TOUCH,-22,-8,22,8)
	if CheckResult==1
		CallFunction(PlayerObject_BadnikBreak)
	end switch
end sub

sub ObjectDraw
	DrawSpriteFX(Object.Frame,FX_FLIP,Object.Xpos,Object.YPos)

	TempValue0=Object.Blades
	TempValue0>>=1
	TempValue0+=2
	DrawSprite(TempValue0)
	Object.Blades++
	if Object.Blades>3
		Object.Blades=0
	end if
end sub

sub ObjectStartup
	LoadSpriteSheet("SSZ/Objects.gif")
	
	//HBomber Moving Frames
	SpriteFrame(-24,-10,47,19,1,1)

	//HBomber Shooting Frames
	SpriteFrame(-24,-10,30,29,1,21)

	//Heli-blades
	SpriteFrame(-16,-18,32,8,1,51)
	SpriteFrame(-16,-18,32,8,1,60)
end sub

sub RSDK
	LoadSpriteSheet("SSZ/Objects.gif")
	SetEditorIcon(Icon0,SingleIcon,-24,-10,47,19,52,1)
end sub