//-----------------Sonic Nexus HUD--------------------//
//----Scripted by Christian Whitehead "The Taxman"----//

#alias 4 : TYPE_HUD

#alias Object.Value0 : Object.ScoreXPos
#alias Object.Value1 : Object.TimeXPos
#alias Object.Value2 : Object.RingXPos
#alias Object.Value3 : Object.LifeXPos
#alias Object.Value4 : FlashingTimer

#alias Object.Value0 : Rings

sub ObjectDraw

	switch Object.State
	case 0

		if Rings[0]==0
			FlashingTimer++
			if FlashingTimer==16
				FlashingTimer=0
			end if
		else
			if Stage.Minutes==9
				FlashingTimer++
				if FlashingTimer==16
					FlashingTimer=0
				end if
			end if
		end if
		//Draw Score
		DrawSpriteScreenXY(10,15,7)

		//Draw Time
		if Stage.Minutes==9
			if FlashingTimer>7
				DrawSpriteScreenXY(13,15,23)
			else
				DrawSpriteScreenXY(11,15,23)
			end if
		else
			DrawSpriteScreenXY(11,15,23)
		end if

		//Draw Rings
		if Rings[0]==0
			if FlashingTimer>7
				DrawSpriteScreenXY(14,15,39)
			else
				DrawSpriteScreenXY(12,15,39)
			end if
		else
			DrawSpriteScreenXY(12,15,39)
		end if
		
		//Draw Seconds Markings
		DrawSpriteScreenXY(15,65,23)

		//Draw Score Numbers
		DrawNumbers(0,103,7,Player.Score,6,8,0)

		//Draw Milliseconds
		DrawNumbers(0,103,23,Stage.MilliSeconds,2,8,1)

		//Draw Seconds
		DrawNumbers(0,79,23,Stage.Seconds,2,8,1)

		//Draw Minutes
		DrawNumbers(0,55,23,Stage.Minutes,1,8,1)

		//Draw Rings
		DrawNumbers(0,79,39,Rings[0],3,8,0)

		//DrawLifeIcon(15,215)
		DrawNumbers(0,40,220,Player.Lives,1,8,1)
		break

	case 1 //HUD Entering

		if Object.ScoreXPos<15
			Object.ScoreXPos+=16
		end if

		if Object.TimeXPos<15
			Object.TimeXPos+=16
		end if

		if Object.RingXPos<15
			Object.RingXPos+=16
		end if

		if Object.LifeXPos<15
			Object.LifeXPos+=16
		else
			Object.State=0
			Stage.TimeEnabled=true
		end if

		//Draw Score
		DrawSpriteScreenXY(10,Object.ScoreXPos,7)
		//Draw Time
		DrawSpriteScreenXY(11,Object.TimeXPos,23)
		//Draw Rings
		DrawSpriteScreenXY(12,Object.RingXPos,39)

		//Draw Seconds Markings
		TempValue0=Object.TimeXPos
		TempValue0+=50
		DrawSpriteScreenXY(15,TempValue0,23)

		//Draw Score Numbers
		TempValue0=Object.ScoreXPos
		TempValue0+=88
		DrawNumbers(0,TempValue0,7,Player.Score,6,8,0)

		//Draw Milliseconds
		TempValue0=Object.TimeXPos
		TempValue0+=88
		DrawNumbers(0,TempValue0,23,Stage.MilliSeconds,2,8,1)

		//Draw Seconds
		TempValue0=Object.TimeXPos
		TempValue0+=64
		DrawNumbers(0,TempValue0,23,Stage.Seconds,2,8,1)

		//Draw Minutes
		TempValue0=Object.TimeXPos
		TempValue0+=40
		DrawNumbers(0,TempValue0,23,Stage.Minutes,1,8,1)

		//Draw Rings
		TempValue0=Object.RingXPos
		TempValue0+=64
		DrawNumbers(0,TempValue0,39,Rings[0],3,8,0)

		//DrawLifeIcon(Object.LifeXPos,215)
		TempValue0=Object.LifeXPos
		TempValue0+=25
		DrawNumbers(0,TempValue0,220,Player.Lives,1,8,1)
		break
	end switch

end sub

sub ObjectStartup
	LoadSpriteSheet("NexusGlobals/Display.gif")
	
	//Setup Any HUD Objects that are present in the level
	//In most cases there won't be any, because
	//the Titlecard spawns the HUD

	ArrayPos0=32
	while ArrayPos0<1056
		if Object[ArrayPos0].Type==TYPE_HUD
			Object[ArrayPos0].Priority=1
			Object[ArrayPos0].DrawOrder=6
		end if
		ArrayPos0++
	loop

	//Numbers 0-9
	SpriteFrame(0,0,9,13,173,305)
	SpriteFrame(0,0,9,13,173,319)
	SpriteFrame(0,0,9,13,173,333)
	SpriteFrame(0,0,9,13,173,347)
	SpriteFrame(0,0,9,13,173,361)
	SpriteFrame(0,0,9,13,183,305)
	SpriteFrame(0,0,9,13,183,319)
	SpriteFrame(0,0,9,13,183,333)
	SpriteFrame(0,0,9,13,183,347)
	SpriteFrame(0,0,9,13,183,361)
	
	//Score Graphic
	SpriteFrame(0,0,41,13,131,305)
	//Time Graphic
	SpriteFrame(0,0,34,13,131,319)
	//Rings Graphic
	SpriteFrame(0,0,41,13,131,333)
	
	//Red Time Graphic
	SpriteFrame(0,0,34,13,131,347)
	//Red Rings Graphic
	SpriteFrame(0,0,41,13,131,361)
	
	//Seconds markings
	SpriteFrame(0,0,31,6,131,375)
end sub

sub RSDK
	LoadSpriteSheet("NexusGlobals/Display.gif")
	SetEditorIcon(Icon0,SingleIcon,-11,-11,22,22,170,375)
end sub