//-----------Sonic Nexus Act Finish Screen------------//
//----Scripted by Christian Whitehead "The Taxman"----//

#alias Object.Value0 : Object.Timer

#alias Object.XPos : Object.LineXPos0
#alias Object.Value1 : Object.LineXPos1
#alias Object.Value2 : Object.LineXPos2
#alias Object.Value3 : Object.LineXPos3
#alias Object.Value4 : Object.LineXPos4
#alias Object.Value5 : Object.TimeBonus
#alias Object.Value6 : Object.RingBonus
#alias Object.Value7 : Object.Total

sub ObjectMain

	switch Object.State
	case 0 //Setup the Line X Positions
		Object.LineXPos0=-799
		Object.LineXPos1=1129
		Object.LineXPos2=1215
		Object.LineXPos3=1279
		Object.LineXPos4=1363
		Object.State=1

		//Setup the Bonuses etc
		switch Stage.Minutes
		case 0
			Object.TimeBonus=10000
			break
		case 1
			Object.TimeBonus=4000
			break
		case 2
			Object.TimeBonus=2000
			break
		case 3
			Object.TimeBonus=1000
			break
		case 4
			Object.TimeBonus=500
			break
		case 5
			Object.TimeBonus=100
			break
		end switch

		Object.RingBonus=Object[0].Value0
		Object.RingBonus*=100

		Object.Total=0
		break

	case 1 //Moving the Text in

		if Object.LineXPos0<132 //SONIC GOT
			Object.LineXPos0+=16
		else
			Object.LineXPos0=132
		end if

		if Object.LineXPos1>139 //THROUGH Zone
			Object.LineXPos1-=16
		else
			Object.LineXPos1=139
		end if

		if Object.LineXPos2>114 //Time Bonus
			Object.LineXPos2-=16
		else
			Object.LineXPos2=114
		end if

		if Object.LineXPos3>114 //Ring Bonus
			Object.LineXPos3-=16
		else
			Object.LineXPos3=114
		end if

		if Object.LineXPos4>134 //Total
			Object.LineXPos4-=16
		else
			Object.LineXPos4=134
			Object.State=2
			Object.Timer=0
		end if

		break

	case 2	//Wait a bit
		Object.Timer++
		if Object.Timer>299
			Object.Timer=0
			Object.State=3
		end if
		break

	case 3 //Add Everying Up and Kaching!

		if Object.RingBonus>0
			Object.RingBonus-=100
			Object.Total+=100
			Player.Score+=100
		end if

		if Object.TimeBonus>0
			Object.TimeBonus-=100
			Object.Total+=100
			Player.Score+=100
		end if

		CheckGreater(Object.RingBonus,0)
		TempValue0=CheckResult
		CheckGreater(Object.TimeBonus,0)
		TempValue0|=CheckResult

		if TempValue0==1
			Object.Timer++
			if Object.Timer==2
				PlaySfx(13,false)
				Object.Timer=0
			end if
		else
			Object.State=4
			Object.Timer=0
			PlaySfx(14,false)
		end if

		break

	case 4	//Wait some More
		Object.Timer++
		if Object.Timer==160
			Object.Timer=0
			Object.State=5
		end if
		break

	case 5	//Fade out and Load Next Stage
		Object.Timer+=4
		//SetFade(0,0,0,Object.Timer,0,255)
		if Object.Timer==384
			Object.Timer=0
			Stage.ListPos++

			if Stage.ListPos<Stage.ListSize
				LoadStage()
			else
				//Back to the Titlescreen
				Stage.ActiveList=PRESENTATION_STAGE
				Stage.ListPos=0
				LoadStage()
			end if
		end if
		break

	end switch

end sub

sub ObjectDraw
	//Draw Sonic Text
	DrawSpriteScreenXY(20,Object.LineXPos0,63)

	//Draw Got Text
	TempValue0=Object.LineXPos0
	TempValue0+=79
	DrawSpriteScreenXY(0,TempValue0,63)

	//Draw Through Text
	DrawSpriteScreenXY(4,Object.LineXPos1,83)

	//Draw Zone Text
	TempValue0=Object.LineXPos1
	TempValue0+=116
	DrawSpriteScreenXY(5,TempValue0,95)

	//Draw Act No
	TempValue0+=38
	DrawSpriteScreenXY(Stage.ActNo,TempValue0,73)

	//Draw Time Text
	DrawSpriteScreenXY(6,Object.LineXPos2,120)

	//Draw Bonus Text
	TempValue0=Object.LineXPos2
	TempValue0+=40
	DrawSpriteScreenXY(8,TempValue0,120)

	//Draw Time Bonus
	TempValue0+=144
	DrawNumbers(10,TempValue0,120,Object.TimeBonus,5,8,0)

	//Draw Ring Text
	DrawSpriteScreenXY(7,Object.LineXPos3,136)

	//Draw Bonus Text
	TempValue0=Object.LineXPos3
	TempValue0+=40
	DrawSpriteScreenXY(8,TempValue0,136)

	//Draw Ring Bonus
	TempValue0+=144
	DrawNumbers(10,TempValue0,136,Object.RingBonus,5,8,0)

	//Draw Total Text
	DrawSpriteScreenXY(9,Object.LineXPos4,164)

	//Draw Total
	TempValue0=Object.LineXPos4
	TempValue0+=164
	DrawNumbers(10,TempValue0,164,Object.Total,5,8,0)

end sub

sub ObjectStartup
	LoadSpriteSheet("NexusGlobals/Display.gif")

	//Got Text
	SpriteFrame(0,0,47,19,430,1)

	//Numbers 1-3
	SpriteFrame(0,0,14,37,355,41)
	SpriteFrame(0,0,20,37,370,41)
	SpriteFrame(0,0,20,37,391,41)

	//Through Text
	SpriteFrame(0,0,112,19,355,21)

	//Zone Text
	SpriteFrame(0,0,36,13,140,221)

	//Time Text
	SpriteFrame(0,0,33,13,131,319)

	//Ring Text
	SpriteFrame(0,0,33,13,131,333)

	//Bonus Text
	SpriteFrame(0,0,43,13,412,41)

	//Total Text
	SpriteFrame(0,0,40,13,412,55)

	//Numbers 0-9
	SpriteFrame(0,0,9,13,173,305)
	SpriteFrame(0,0,9,13,173,319)
	SpriteFrame(0,0,9,13,173,333)
	SpriteFrame(0,0,9,13,173,347)
	SpriteFrame(0,0,9,13,173,361)
	SpriteFrame(0,0,9,13,183,305)
	SpriteFrame(0,0,9,13,183,319)
	SpriteFrame(0,0,9,13,183,333)
	SpriteFrame(0,0,9,13,183,347)
	SpriteFrame(0,0,9,13,183,361)

	//Sonic Text
	SpriteFrame(0,0,74,19,355,1)

end sub

sub RSDK
	LoadSpriteSheet("NexusGlobals/Display.gif")
	SetEditorIcon(Icon0,SingleIcon,-15,-15,30,30,193,347)
end sub