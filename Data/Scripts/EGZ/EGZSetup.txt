//-------Retro Sonic EGZ Setup Script Recreation--------//
//--------------Scripted by LanceReborn-------------//

//-------Aliases-------//
#alias 45: TYPE_EGZSETUP


sub ObjectMain
	object.value2++
	if object.value2==4
		object.value2=0
		RotatePalette(252, 255, 1)
	endif
endsub


sub ObjectDraw
	Object.Value1++
	if Object.Value1>3
		TileLayer[0].DeformationOffsetW++
		TileLayer[4].DeformationOffset++
		Object.Value1=0
	endif
	if Stage[0].ActiveLayer==4
		Object.Value0+=4 //Hell nah im not doing my own 3d scrolling sky
		Object.Value0&=2047
		TempValue0=Object.Value0
		FlipSign(TempValue0)
		MatrixTranslateXYZ(MAT_WORLD,-512,720,TempValue0)
		Draw3DScene()
	endif
endsub


sub ObjectStartup
	if Stage.DebugMode==1
		LoadSpriteSheet("EGZ/Test.gif")
	else
		LoadSpriteSheet("EGZ/Clouds.gif")
	endif
	3DScene.NoVertices=0 //I hope taxman does not care about me stealing the clouds script :Happy_Emoji:
	3DScene.NoFaces=0
	ArrayPos0=3DScene.NoVertices
	ArrayPos1=3DScene.NoFaces
	MatrixTranslateXYZ(MAT_WORLD,0,0,0)
	MatrixRotateXYZ(MAT_WORLD,0,0,0)
	MatrixRotateXYZ(MAT_VIEW,8,0,0)
	ArrayPos0=0
	ArrayPos1=0
	TempValue2=0
	TempValue3=0
	TempValue4=0
	TempValue5=0
	while TempValue3<20
		TempValue0=0
		TempValue1=-8192
		TempValue4=0
		while TempValue0<16
			VertexBuffer[ArrayPos0].x=TempValue1
			VertexBuffer[ArrayPos0].y=0
			VertexBuffer[ArrayPos0].z=TempValue2
			VertexBuffer[ArrayPos0].u=TempValue4
			VertexBuffer[ArrayPos0].v=TempValue5
			ArrayPos0++
			TempValue2+=512
			VertexBuffer[ArrayPos0].x=TempValue1
			VertexBuffer[ArrayPos0].y=0
			VertexBuffer[ArrayPos0].z=TempValue2
			VertexBuffer[ArrayPos0].u=TempValue4
			VertexBuffer[ArrayPos0].v=TempValue5
			VertexBuffer[ArrayPos0].v+=63
			ArrayPos0++
			TempValue2-=512
			TempValue1+=1024
			VertexBuffer[ArrayPos0].x=TempValue1
			VertexBuffer[ArrayPos0].y=0
			VertexBuffer[ArrayPos0].z=TempValue2
			VertexBuffer[ArrayPos0].u=TempValue4
			VertexBuffer[ArrayPos0].u+=63
			VertexBuffer[ArrayPos0].v=TempValue5
			ArrayPos0++
			TempValue2+=512
			VertexBuffer[ArrayPos0].x=TempValue1
			VertexBuffer[ArrayPos0].y=0
			VertexBuffer[ArrayPos0].z=TempValue2
			VertexBuffer[ArrayPos0].u=TempValue4
			VertexBuffer[ArrayPos0].u+=63
			VertexBuffer[ArrayPos0].v=TempValue5
			VertexBuffer[ArrayPos0].v+=63
			TempValue4+=64
			TempValue4&=255
			TempValue2-=512
			ArrayPos0-=3
			FaceBuffer[ArrayPos1].a=ArrayPos0
			FaceBuffer[ArrayPos1].b=ArrayPos0
			FaceBuffer[ArrayPos1].b+=2
			FaceBuffer[ArrayPos1].c=ArrayPos0
			FaceBuffer[ArrayPos1].c+=1
			FaceBuffer[ArrayPos1].d=ArrayPos0
			FaceBuffer[ArrayPos1].d+=3
			FaceBuffer[ArrayPos1].Flag=0
			ArrayPos0+=4
			ArrayPos1++
			TempValue0++
			3DScene.NoVertices+=4
			3DScene.NoFaces++
		loop
		TempValue5+=64
		TempValue5&=127
		TempValue2+=512
		TempValue3++
	loop
	Object[5].Type=TypeName[EGZSetup]
	Object[5].Priority=1
	Object[5].DrawOrder=2
	SetMusicTrack("RetroSonic/EggGarden.ogg",0,1)
	SetLayerDeformation(2,128,4,0,0,0)
endsub

sub RSDK
	LoadSpriteSheet("Global/Display.gif")
	SetEditorIcon(Icon0,SingleIcon,-16,-16,32,32,1,143)
endsub
